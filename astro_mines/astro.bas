   10 REM ******************************
   20 REM *        Astro Mines         *
   30 REM *  This Version Is For The   *
   40 REM *      **Agon Light**        *
   50 REM *                            *
   60 REM *     By Steve Lovejoy       *
   70 REM *         July 2024          *
   80 REM ******************************
   90 REM *         Main Code          *
  100 REM
  110 SC=0 : HS=0 : STA=0 : speed%=100
  120 MODE 7
  130 PROC_HideCursor
  140 FMAX=7.9
  150 PROCUSR : PROCINIT : PROCINTRO
  160 MODE 8 : FMIN=1.5 : SHFUEL=0.2 : MOFUEL=0.1 : CAFUEL=2.5 : FUEL=FMAX : A=5 : AA1=0 : PROCSCRN
  170 PROC_HideCursor : CT=1
  180 :
  190 PROCGUAGE
  200 PROCSC
  210 REM print laser bullet
  220 IF ASC A$=13 THEN L=1 : FUEL=FUEL-SHFUEL : X=X% : Y=Y% : Q=Q% : W=W% : SOUND 0,1,-10,1 : FOR N=1 TO 1000 : COLOUR 10 : PRINTTAB(X,Y);CHR$(241)
  230 REM print main space ship on screen.
  240 COLOUR 14 : PRINTTAB(X%,Y%);A$(R%)
  250 REM print space ship on map.
  260 PRINTTAB(26,26);A$(R%) : JK=JK+1
  270 REM fuel guage
  280 IF JK/31 = INT(JK/31) THEN PROCGUAGE
  288 IF FUEL=>FMAX THEN FUEL=FMAX
  290 IF FUEL=<FMIN THEN PROCEND : MODE 7 : PROC_HideCursor : PROCINTRO : GOTO 160
  300 Q%=Q%(R%) : W%=W%(R%)
  310 IF X%=0 AND Q%=-1 THEN Q%=0
  320 IF X%=39 AND Q%=1 THEN Q%=0
  330 IF Y%=20 AND W%=1 THEN W%=0
  340 IF Y%=1 AND W%=-1 THEN W%=0
  350 REM print mine on map.
  360 IF B(X%+Q%,Y%+W%)=1 THEN VDU 5 : MOVE 600,156 : PRINTCHR$(238) : VDU 4 : ELSE GCOL0,0 : VDU 5 : MOVE 600,156 : PRINTCHR$(238) : VDU 4 : GCOL0,15
  370 REM collect cargo
  380 COLOUR 15
  390 IF B(X%,Y%)=2 THEN SC=SC+10 : PROCSC : SOUND1,3,50,1 : AA1=AA1+1 : FUEL=FUEL+CAFUEL : PRINTTAB(9,22);AA1 : B(X%,Y%)=0 : FOR C=1 TO 100 : NEXT C : IF AA1=A THEN PROCGAL : AA1=0 : VDU 4
  400 IF B(X%,Y%)=1 THEN PROCEND : MODE 7 : PROC_HideCursor : PROCINTRO : GOTO 160
  410 A$=INKEY$(0)
  420 IF A$="Z" OR A$="z" THEN : R%=R%-1 : IF R%=0 THEN R%=8
  430 IF A$="X" OR A$="x" THEN : R%=R%+1 : IF R%=9 THEN R%=1
  440 REM alien
  450 IF G=1 THEN 480
  460 J=INT(RND(200)) : IF J=1 THEN G=1 : GX=INT(RND(39)) : GY=INT(RND(20)) : GOTO 610
  470 GOTO 700
  480 PRINTTAB(GX,GY);" " : B(GX,GY)=1
  490 SOUND1,3,100,1
  500 r%=INT(RND(speed%)) : REM alien speed.
  510 IF r%=5 THEN 520 ELSE 620
  520 IF GX<X% THEN G1=1
  530 IF GX>X% THEN G1=-1
  540 IF GX=X% THEN G1=0
  550 IF GY<Y% THEN G2=1
  560 IF GY>Y% THEN G2=-1
  570 IF GY=Y% THEN G2=0
  580 IF GX=X AND GY=Y AND L THEN 670
  590 GT=GT+1
  600 IF GT/2=INT(GT/2)GX=GX+G1 : GY=GY+G2
  610 T=T+1 : IF T=3 THEN T=1
  620 COLOUR 9 : PRINTTAB(GX,GY);G$(T)
  630 IF B(GX,GY)=2 THEN SOUND1,1,0,1 : COLOUR 15 : PRINTTAB(GX,GY);"*" : G=0
  640 REM alien collision, death.
  650 IF GX=X% AND GY=Y% THEN PROCEND : MODE 7 : PROC_HideCursor : PROCINTRO : GOTO 160
  660 IF L=0 THEN 700
  670 IF INT(GX)=X AND INT(GY)=Y AND L=1 THEN SC=SC+20 : PROCSC : SOUND1,1,0,1 : PRINTTAB(GX,GY);" " : G=0 : L=0 : N=1000 : NEXT
  680 :
  690 REM move main ship
  700 IF A$=" " THEN PRINTTAB(X%,Y%);" " : X%=X%+Q% : Y%=Y%+W% : PRINTTAB(X%,Y%);A$(R%) : FUEL=FUEL-MOFUEL : SOUND0,4,10,1
  710 IF L=0 THEN 760
  720 IF X=38 OR X=1 OR Y=1 OR Y=20 THEN N=1000 : PRINTTAB(X,Y);" " : NEXT N : L=0
  730 IF B(X+Q,Y+W)=2 THEN N=1000 : PRINTTAB(X,Y);" " : NEXT N : L=0
  740 IF L=0 THEN 760
  750 PRINTTAB(X,Y);" " : X=X+Q : Y=Y+W : NEXT : L=0
  760 GOTO 220
  770 :
  780 DEF PROCUSR
  790 IF STA=1 THEN ENDPROC
  800 VDU 23,224,16,56,124,254,254,238,198,130 : REM spaceship N
  810 VDU 23,225,31,63,127,255,15,14,12,8 : REM spaceship W
  820 VDU 23,226,248,124,62,31,62,124,248,0 : REM spaceship E
  830 VDU 23,227,8,12,14,15,255,127,63,31 : REM spaceship SE
  840 VDU 23,228,65,99,119,127,127,62,28,8 : REM spaceship S
  850 VDU 23,229,16,48,112,240,255,254,252,248 : REM spaceship SW
  860 VDU 23,230,0,31,62,124,248,124,62,31 : REM spaceship NE
  870 VDU 23,231,248,252,254,255,240,112,48,16 : REM spaceship NW
  880 :
  890 VDU 23,232,255,137,72,63,31,1,59,31 : REM s mine detector
  900 VDU 23,233,248,6,129,200,228,55,152,255 : REM s mine detector
  910 VDU 23,234,0,0,128,96,24,134,1,255 : REM s mine detector
  920 VDU 23,235,59,1,31,63,72,137,255,0 : REM s1 mine detector
  930 VDU 23,236,152,55,228,200,129,6,248,0 : REM s1 mine detector
  940 VDU 23,237,1,134,24,96,128,0,0,0 : REM s1 mine detector
  950 :
  960 VDU 23,238,0,60,126,126,126,126,60,0 : REM mine
  970 :
  980 VDU 23,239,130,68,56,40,56,68,130,0 : REM alien
  990 VDU 23,240,16,16,56,238,56,16,16,0 : REM alien
 1000 VDU 23,241,0,0,0,24,24,0,0,0 : REM laser bullet
 1010 VDU 23,255,255,255,255,255,255,255,255,255 : REM solid block
 1020 DIM A$(8) : FOR I=1 TO 8 : A$(I)=CHR$(I+223) : NEXT : REM set main spaceship chars to dim A$
 1030 :
 1040 S$=CHR$(232)+CHR$(233)+CHR$(234) : S1$=CHR$(235)+CHR$(236)+CHR$(237) : REM set radar spaceship to chars S% + S1%
 1050 ENDPROC
 1060 :
 1070 DEF PROCINIT
 1080 G1=0:G2=0:GX=0:GY=0:Q=0:W=0:X=0:Y=0:G=0:A=5:A1=0:A$="":L=0:R%=1:X%=10:Y%=10:Q%=0:W%=-1
 1090 DATA0,-1,1,-1,1,0,1,1,0,1,-1,1,-1,0,-1,-1
 1100 IF STA=0 THEN RESTORE 1090 : DIMQ%(8),W%(8),B(39,29),G$(2) : FOR I=1 TO 8 : READQ%(I),W%(I) : NEXT
 1110 T=1 : GAL=1 : G$(1)=CHR$(239) : G$(2)=CHR$(240)
 1120 FUEL=FMAX : JK=1 : SF=SINFUEL : CF=COSFUEL : SC=0
 1130 ENDPROC
 1140 :
 1150 DEF PROCSCRN
 1160 PROC_HideCursor : PRINTTAB(15,14);"PLEASE WAIT" : REM PROC_delay(500)
 1170 FOR I%=1 TO 39 : FOR J%= 1 TO 29 : B(I%,J%)=0 : NEXT : NEXT
 1180 FOR I%=1 TO RND(50)+100
 1190   B(RND(39),RND(20))=1
 1200 NEXT
 1210 CLS
 1220 :
 1230 REM cargo
 1240 FOR I=1 TO A : COLOUR 15 : M=INT(RND(38)+1) : L=INT(RND(18)+1) : PRINTTAB(M,L);"*" : B(M,L)=2 : NEXT : L=0
 1250 X%=19 : Y%=10 : IF B(X%,Y%)=1 THEN B(X%,Y%)=0
 1260 :
 1270 REM main cockpit frame
 1280 GCOL0,10
 1290 MOVE 0,310:PLOT1,1279,0:PLOT1,0,-300:PLOT1,-1279,0:PLOT1,0,300
 1300 MOVE 0,310:PLOT1,25,25:PLOT1,1229,0:PLOT1,25,-25 : FOR I=20 TO 1259 STEP 20 : MOVE I,310 : PLOT1,0,20 : NEXT
 1310 :
 1320 REM mine radar spaceship
 1330 COLOUR 14
 1340 PRINTTAB(15,25);S$;TAB(15,26);S1$
 1350 :
 1360 REM mine radar spaceship box
 1370 GCOL0,10 : COLOUR 15
 1380 MOVE 445,192:PLOT1,220,0:PLOT1,0,-110:PLOT1,-220,0:PLOT1,0,110
 1390 :
 1400 REM cargo & boxes
 1410 PRINTTAB(2,22);"CARGO   ";A1
 1420 MOVE 45,232:PLOT1,350,0:PLOT1,0,50:PLOT1,-350,0:PLOT1,0,-50
 1430 MOVE 35,222:PLOT1,370,0:PLOT1,0,70:PLOT1,-370,0:PLOT1,0,-70
 1440 REM galaxy & boxes
 1450 PRINTTAB(2,25);"GALAXY  ";GAL
 1460 MOVE 45,132:PLOT1,350,0:PLOT1,0,50:PLOT1,-350,0:PLOT1,0,-50
 1470 MOVE 35,122:PLOT1,370,0:PLOT1,0,70:PLOT1,-370,0:PLOT1,0,-70
 1480 REM score & boxes
 1490 PRINTTAB(2,28);"SCORE "
 1500 MOVE 45,32:PLOT1,350,0:PLOT1,0,50:PLOT1,-350,0:PLOT1,0,-50
 1510 MOVE 35,22:PLOT1,370,0:PLOT1,0,70:PLOT1,-370,0:PLOT1,0,-70
 1520 REM energy & box
 1530 PRINTTAB(33,22);"FUEL"
 1540 MOVE 1010,232:PLOT1,0,50:PLOT1,210,0:PLOT1,0,-50:PLOT1,-210,0
 1550 REM energy dial
 1560 MOVE 1020,20:PLOT69,1020,20:PLOT85,1020,220:PLOT85,1220,20:PLOT85,1220,220
 1570 GCOL0,0 : PLOT69,1120,120 : FOR U=1 TO 7.4 STEP.1 : PLOT 69,1120,120:PLOT85,1120+(COSU*90),120+(SINU*90) : NEXT
 1580 :
 1590 REM map
 1600 GCOL0,15
 1610 MOVE 845,40:PLOT1,-80,80:PLOT1,80,80:PLOT1,80,-80:PLOT1,-80,-80:PLOT1,0,155
 1620 MOVE 765,120:PLOT1,155,0
 1630 MOVE 845,60:PLOT1,-60,60:PLOT1,60,60:PLOT1,60,-60:PLOT1,-60,-60
 1640 :
 1650 VDU 4 : PRINTTAB(26,26);"" : REM "Â€"
 1660 GCOL0,10
 1670 PRINTTAB(22,22);"POSITION"
 1680 MOVE 690,232:PLOT1,280,0:PLOT1,0,50:PLOT1,-280,0:PLOT1,0,-50
 1690 PRINTTAB(15,22);"RADAR"
 1700 MOVE 445,232:PLOT1,220,0:PLOT1,0,50:PLOT1,-220,0:PLOT1,0,-40
 1710 VDU4
 1720 ENDPROC
 1730 :
 1740 DEF PROCGUAGE
 1750 VDU 4 : GCOL0,0 : MOVE1120,120:PLOT1,70*CF,70*SF : GCOL0,1 : SF=SINFUEL : CF=COSFUEL : MOVE1120,120 : PLOT1,70*CF,70*SF
 1760 ENDPROC
 1770 :
 1780 DEF PROCEND
 1790 STA=1
 1800 SOUND1,0,0,0 : SOUND2,0,0,0 : SOUND3,0,0,1
 1810 FOR J=1 TO 3
 1820   X1=X%:X2=X%:X3=X%:X4=X%:Y1=Y%:Y2=Y%:Y3=Y%:Y4=Y%
 1830   Z=-15
 1840   REPEAT
 1850     A=INT(RND(4)) : IF A=2 THEN VDU 19,0,RND(7)+7,0,0,0
 1860     PRINTTAB(X1,Y1);".";TAB(X2,Y2);".";TAB(X3,Y3);".";TAB(X4,Y4);"."
 1870     SOUND0,Z,-2,4 : Z=Z+1
 1880     FOR G=1 TO 100 : NEXT G
 1890     PRINTTAB(X1,Y1);" ";TAB(X2,Y2);" ";TAB(X3,Y3);" ";TAB(X4,Y4);" "
 1900     X1=X1-1 : X2=X2-1 : X3=X3+1:X4=X4+1:Y1=Y1-1:Y2=Y2+1:Y3=Y3-1:Y4=Y4+1
 1910   UNTIL X1=1 OR X3=1 OR X2=38 OR X4=38 OR Y1=1 OR Y2=21 OR Y3=1 OR Y4=21
 1920   FOR U=1 TO 500 : NEXT : NEXT
 1930 VDU 19,0,0,0,0,0
 1940 FOR G=1 TO 500 : NEXT G : ENDPROC
 1950 :
 1960 DEF PROCGAL
 1970 A1=0 : A=A+1 : GAL=GAL+1 : CLS : FUEL=FMAX : SOUND 1,3,52,100 : SOUND 2,3,50,100 : PROCSCRN : PROCGUAGE : ENDPROC
 1980 :
 1990 REM print score.
 2000 DEF PROCSC : GCOL0,0 : VDU 5 : MOVE 250,65 : PRINTCHR$(255);CHR$(255);CHR$(255);CHR$(255) : GCOL0,15 : MOVE30,65 : PRINTSC : VDU 4 : ENDPROC
 2010 :
 2020 DEF PROCINTRO
 2030 PRINTTAB(0,0)
 2040 R=129 : Y=131 : FOR I=1 TO 2 : PRINTSPC(8); : VDU 141,Y,65,R,83,Y,84,R,82,Y,79,135,45,Y,77,R,73,Y,78,R,69,Y,83,135 : PRINT : NEXT
 2050 IF SC>HS HS=SC
 2060 PRINT : D$=CHR$(141)+" HIGH SCORE "+STR$HS
 2070 PRINTTAB(12);D$ : PRINTTAB(12);D$
 2080 D$=CHR$(141)+CHR$(134)+" LAST SCORE "+STR$SC
 2090 PRINTTAB(11);D$ : PRINTTAB(11);D$
 2100 PRINT : PRINTTAB(6);CHR$(Y)"WRITTEN BY GRAHAM WALKDEN"
 2110 PRINT : PRINTTAB(7);CHR$(Y)"PORTED TO THE AGON LIGHT"
 2120 PRINT : PRINTTAB(11);CHR$(Y)"BY STEVE LOVEJOY"
 2130 PRINT : PRINT : PRINT : PRINT
 2150 PRINT'CHR$(R);"PRESS";CHR$(Y);"P";CHR$(R);"TO PLAY OR";CHR$(Y);"I";CHR$(R);"FOR INSTRUCTIONS"
 2160 A$=INKEY$(10000)
 2170 IF A$="P" OR A$="p" THEN STA=1 : PROCINIT : A=5 : ENDPROC
 2180 CLS
 2190 PRINT : D$=CHR$(141)+CHR$(Y)+"INSTRUCTIONS" : Y=1 : FOR J=1 TO 2 : PROCTELETEXT : NEXT
 2200 RESTORE 2270 : FOR J=1 TO 15 : READD$ : A$=INKEY$(0) : PROCTELETEXT : NEXT : Y=Y+1 : D$=CHR$(141)+CHR$(131)+"PRESS ANY KEY" : FOR J=1 TO 2 : PROCTELETEXT : NEXT
 2210 AB=GET : CLS : Y=1 : FOR I=1 TO 7 : READD$ : PROCTELETEXT : NEXT : Y=Y+2 : D$=CHR$(141)+CHR$(131)+"PRESS ANY KEY" : FOR J=1 TO 2 : PROCTELETEXT : NEXT
 2220 AB=GET : CLS
 2225 PRINT : D$=CHR$(141)+CHR$(131)+"CONTROLS" : Y=1 : FOR J=1 TO 2 : PROCTELETEXT : NEXT
 2230 FOR J=1 TO 7 : READD$ : PROCTELETEXT : NEXT
 2240 Y1=131 : PRINT' : D$=CHR$(R)+"PRESS"+CHR$(Y1)+"Z"+CHR$(R)+"TO ROTATE LEFT" : PROCTELETEXT : D$=CHR$(R)+"PRESS"+CHR$(Y1)+"X"+CHR$(R)+"TO ROTATE RIGHT":PROCTELETEXT:D$=CHR$(R)+"PRESS"+CHR$(Y1)+"SPACE"+CHR$(R)+"TO MOVE FORWARD" : PROCTELETEXT
 2250 D$=CHR$(R)+"PRESS"+CHR$(Y1)+"<ENTER>"+CHR$(R)+"TO FIRE" : PROCTELETEXT : PRINT''' : D$=CHR$(141)+CHR$(131)+"PRESS"+CHR$(129)+"P"+CHR$(131)+"TO PLAY" : Y=Y+2 : FOR J=1 TO 2 : PROCTELETEXT : NEXT
 2260 A$=GET$ : CLS : GOTO 2030
 2270 DATA"You are the captain of the starship","'GALAXY HOPPER' and your mission is to","search the galaxy for planets suitable","for mining. You must clear each galaxy","before you are destroyed by aliens, or","you run out of FUEL."," "
 2280 DATA"To make matters worse, the star systems","have been mined by the aliens, and","the mines are highly dangerous to your","ship, though your photon torpedoes and","alien space-craft will pass through","unscathed."," "
 2290 DATA"","To play, use the keys mentioned later","to position your ship on top of the","planets ( asterisks ). As you clear","each screen, the game will become","increasingly harder, with more planets."
 2300 DATA"Pay particular attention to your FUEL."
 2310 DATA""
 2320 DATA"Your space-craft is equipped with a mine","RADAR situated in the centre of the","control panel. When a mine is detected","in front of you, a RED circle will","appear in front of the imitation space","ship on the RADAR."," "
 2330 END
 2340 :
 2350 DEF PROCTELETEXT : X=(39-LEND$)/2
 2360 PRINTTAB(X,Y);D$ : Y=Y+1 : ENDPROC
 2370 :
 2380 DEF PROC_HideCursor : REM Hide Cursor.
 2390 VDU 23,1,0;0;0;0;
 2400 ENDPROC
 2410 :
 2420 DEF PROC_delay(T%)
 2430 FOR k=1 TO T% : NEXT k
 2440 ENDPROC
