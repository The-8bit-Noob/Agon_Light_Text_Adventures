   10 REM ******************************
   20 REM *        Astro Mines         *
   30 REM *  This Version Is For The   *
   40 REM *      **Agon Light**        *
   50 REM *                            *
   60 REM *     By Steve Lovejoy       *
   70 REM *         July 2024          *
   80 REM ******************************
   90 REM *         Main Code          *
  100 REM
  110 SC=0 : HS=0 : STA=0 : speed%=100
  120 MODE 7
  130 PROC_HideCursor
  140 REM ONERROR RUN
  150 PROCUSR : PROCINIT : PROCINTRO
  160 MODE 8 : FUEL=7.9 : A=5 : AA1=0 : PROCSCRN
  170 PROC_HideCursor : CT=1
  180 :
  190 PROCGUAGE
  200 PROCSC
  210 REM print laser bullet
  220 IF ASC A$=13 THEN L=1 : X=X% : Y=Y% : Q=Q% : W=W% : SOUND 0,1,-10,1 : FOR N=1 TO 1000 : COLOUR 10 : PRINTTAB(X,Y);CHR$(241)
  230 REM print main space ship on screen.
  250 COLOUR 14 : PRINTTAB(X%,Y%);A$(R%)
  260 REM print space ship on map.
  270 PRINTTAB(26,26);A$(R%) : JK=JK+1
  280 REM fuel guage
  290 IF JK/31 = INT(JK/31) THEN PROCGUAGE
  300 IF FUEL=1.53299997 THEN PROCEND : MODE 7 : PROC_HideCursor : PROCINTRO : GOTO 160
  310 Q%=Q%(R%) : W%=W%(R%)
  320 IF X%=0 AND Q%=-1 THEN Q%=0
  330 IF X%=39 AND Q%=1 THEN Q%=0
  340 IF Y%=20 AND W%=1 THEN W%=0
  350 IF Y%=1 AND W%=-1 THEN W%=0
  360 REM print mine on map.
  370 IF B(X%+Q%,Y%+W%)=1 THEN VDU 5 : MOVE 600,156 : PRINTCHR$(238) : VDU 4 : ELSE GCOL0,0 : VDU 5 : MOVE 600,156 : PRINTCHR$(238) : VDU 4 : GCOL0,15
  380 REM collect cargo
  390 COLOUR 15
  400 IF B(X%,Y%)=2 THEN SC=SC+10 : PROCSC : SOUND1,3,50,1 : AA1=AA1+1 : PRINTTAB(9,22);AA1 : B(X%,Y%)=0 : FOR C=1 TO 100 : NEXT C : IF AA1=A THEN PROCGAL : AA1=0 : VDU 4
  410 IF B(X%,Y%)=1 THEN PROCEND : MODE 7 : PROC_HideCursor : PROCINTRO : GOTO 160
  420 A$=INKEY$(0)
  430 IF A$="Z" THEN R%=R%-1 : IF R%=0 THEN R%=8
  440 IF A$="X" THEN R%=R%+1 : IF R%=9 THEN R%=1
  450 REM alien
  460 IF G=1 THEN 490
  470 J=INT(RND(200)) : IF J=1 THEN G=1 : GX=INT(RND(39)) : GY=INT(RND(20)) : GOTO 620
  480 GOTO 720
  490 PRINTTAB(GX,GY);" " : B(GX,GY)=1
  500 SOUND1,3,100,1
  510 r%=INT(RND(speed%)) : REM alien speed.
  520 IF r%=5 THEN 530 ELSE 630
  530 IF GX<X% THEN G1=1
  540 IF GX>X% THEN G1=-1
  550 IF GX=X% THEN G1=0
  560 IF GY<Y% THEN G2=1
  570 IF GY>Y% THEN G2=-1
  580 IF GY=Y% THEN G2=0
  590 IF GX=X AND GY=Y AND L THEN 690
  600 GT=GT+1
  610 IF GT/2=INT(GT/2)GX=GX+G1 : GY=GY+G2
  620 T=T+1 : IF T=3 THEN T=1
  630 COLOUR 9 : PRINTTAB(GX,GY);G$(T)
  650 IF B(GX,GY)=2 THEN SOUND1,1,0,1 : COLOUR 15 : PRINTTAB(GX,GY);"*" : G=0
  660 REM alien collision, death.
  670 IF GX=X% AND GY=Y% THEN PROCEND : MODE 7 : PROC_HideCursor : PROCINTRO : GOTO 160
  680 IF L=0 THEN 720
  690 IF INT(GX)=X AND INT(GY)=Y AND L=1 THEN SC=SC+20 : PROCSC : SOUND1,1,0,1 : PRINTTAB(GX,GY);" " : G=0 : L=0 : N=1000 : NEXT
  700 :
  710 REM move main ship
  720 IF A$=" " THEN PRINTTAB(X%,Y%);" " : X%=X%+Q% : Y%=Y%+W% : PRINTTAB(X%,Y%);A$(R%) : SOUND0,4,10,1
  730 IF L=0 THEN 780
  740 IF X=38 OR X=1 OR Y=1 OR Y=20 THEN N=1000 : PRINTTAB(X,Y);" " : NEXT N : L=0
  750 IF B(X+Q,Y+W)=2 THEN N=1000 : PRINTTAB(X,Y);" " : NEXT N : L=0
  760 IF L=0 THEN 780
  770 PRINTTAB(X,Y);" " : X=X+Q : Y=Y+W : NEXT : L=0
  780 GOTO 220
  790 :
  800 DEF PROCUSR
  810 IF STA=1 THEN ENDPROC
  820 VDU 23,224,16,56,124,254,254,238,198,130 : REM spaceship N
  830 VDU 23,225,31,63,127,255,15,14,12,8 : REM spaceship W
  840 VDU 23,226,248,124,62,31,62,124,248,0 : REM spaceship E
  850 VDU 23,227,8,12,14,15,255,127,63,31 : REM spaceship SE
  860 VDU 23,228,65,99,119,127,127,62,28,8 : REM spaceship S
  870 VDU 23,229,16,48,112,240,255,254,252,248 : REM spaceship SW
  880 VDU 23,230,0,31,62,124,248,124,62,31 : REM spaceship NE
  890 VDU 23,231,248,252,254,255,240,112,48,16 : REM spaceship NW
  900 :
  910 VDU 23,232,255,137,72,63,31,1,59,31 : REM s mine detector
  920 VDU 23,233,248,6,129,200,228,55,152,255 : REM s mine detector
  930 VDU 23,234,0,0,128,96,24,134,1,255 : REM s mine detector
  940 VDU 23,235,59,1,31,63,72,137,255,0 : REM s1 mine detector
  950 VDU 23,236,152,55,228,200,129,6,248,0 : REM s1 mine detector
  960 VDU 23,237,1,134,24,96,128,0,0,0 : REM s1 mine detector
  970 :
  980 VDU 23,238,0,60,126,126,126,126,60,0 : REM mine
  990 :
 1000 VDU 23,239,130,68,56,40,56,68,130,0 : REM alien
 1010 VDU 23,240,16,16,56,238,56,16,16,0 : REM alien
 1020 VDU 23,241,0,0,0,24,24,0,0,0 : REM laser bullet
 1030 VDU 23,255,255,255,255,255,255,255,255,255 : REM solid block
 1040 DIM A$(8) : FOR I=1 TO 8 : A$(I)=CHR$(I+223) : NEXT : REM set main spaceship chars to dim A$
 1050 :
 1060 S$=CHR$(232)+CHR$(233)+CHR$(234) : S1$=CHR$(235)+CHR$(236)+CHR$(237) : REM set radar spaceship to chars S% + S1%
 1070 ENDPROC
 1080 :
 1090 DEF PROCINIT
 1100 G1=0:G2=0:GX=0:GY=0:Q=0:W=0:X=0:Y=0:G=0:A=5:A1=0:A$="":L=0:R%=1:X%=10:Y%=10:Q%=0:W%=-1
 1110 DATA0,-1,1,-1,1,0,1,1,0,1,-1,1,-1,0,-1,-1
 1120 IF STA=0 THEN RESTORE 1110 : DIMQ%(8),W%(8),B(39,29),G$(2) : FOR I=1 TO 8 : READQ%(I),W%(I) : NEXT
 1130 T=1 : GAL=1 : G$(1)=CHR$(239) : G$(2)=CHR$(240)
 1140 FUEL=7.9 : JK=1 : SF=SINFUEL : CF=COSFUEL : SC=0
 1150 ENDPROC
 1160 :
 1170 DEF PROCSCRN
 1180 PROC_HideCursor : PRINTTAB(15,14);"PLEASE WAIT" : REM PROC_delay(500)
 1190 FOR I%=1 TO 39 : FOR J%= 1 TO 29 : B(I%,J%)=0 : NEXT : NEXT
 1200 FOR I%=1 TO RND(50)+100
 1210   B(RND(39),RND(20))=1
 1220 NEXT
 1230 CLS
 1240 :
 1250 REM cargo
 1260 FOR I=1 TO A : COLOUR 15 : M=INT(RND(38)+1) : L=INT(RND(18)+1) : PRINTTAB(M,L);"*" : B(M,L)=2 : NEXT : L=0
 1270 X%=19 : Y%=10 : IF B(X%,Y%)=1 THEN B(X%,Y%)=0
 1280 :
 1290 REM main cockpit frame
 1300 GCOL0,10
 1310 MOVE 0,310:PLOT1,1279,0:PLOT1,0,-300:PLOT1,-1279,0:PLOT1,0,300
 1320 MOVE 0,310:PLOT1,25,25:PLOT1,1229,0:PLOT1,25,-25 : FOR I=20 TO 1259 STEP 20 : MOVE I,310 : PLOT1,0,20 : NEXT
 1330 :
 1340 REM mine detector spaceship
 1350 COLOUR 14
 1360 PRINTTAB(15,25);S$;TAB(15,26);S1$
 1370 :
 1380 REM mine detector spaceship box
 1390 GCOL0,10 : COLOUR 15
 1400 MOVE 445,192:PLOT1,220,0:PLOT1,0,-110:PLOT1,-220,0:PLOT1,0,110
 1410 :
 1420 REM cargo & boxes
 1430 PRINTTAB(2,22);"CARGO   ";A1
 1440 MOVE 45,232:PLOT1,350,0:PLOT1,0,50:PLOT1,-350,0:PLOT1,0,-50
 1450 MOVE 35,222:PLOT1,370,0:PLOT1,0,70:PLOT1,-370,0:PLOT1,0,-70
 1460 REM galaxy & boxes
 1470 PRINTTAB(2,25);"GALAXY  ";GAL
 1480 MOVE 45,132:PLOT1,350,0:PLOT1,0,50:PLOT1,-350,0:PLOT1,0,-50
 1490 MOVE 35,122:PLOT1,370,0:PLOT1,0,70:PLOT1,-370,0:PLOT1,0,-70
 1500 REM score & boxes
 1510 PRINTTAB(2,28);"SCORE "
 1520 MOVE 45,32:PLOT1,350,0:PLOT1,0,50:PLOT1,-350,0:PLOT1,0,-50
 1530 MOVE 35,22:PLOT1,370,0:PLOT1,0,70:PLOT1,-370,0:PLOT1,0,-70
 1540 REM energy & box
 1550 PRINTTAB(32,22);"ENERGY"
 1560 MOVE 1010,232:PLOT1,0,50:PLOT1,210,0:PLOT1,0,-50:PLOT1,-210,0
 1570 REM energy dial
 1580 MOVE 1020,20:PLOT69,1020,20:PLOT85,1020,220:PLOT85,1220,20:PLOT85,1220,220
 1590 GCOL0,0 : PLOT69,1120,120 : FOR U=1 TO 7.4 STEP.1 : PLOT 69,1120,120:PLOT85,1120+(COSU*90),120+(SINU*90) : NEXT
 1600 :
 1610 REM map
 1620 GCOL0,15
 1630 MOVE 845,40:PLOT1,-80,80:PLOT1,80,80:PLOT1,80,-80:PLOT1,-80,-80:PLOT1,0,155
 1640 MOVE 765,120:PLOT1,155,0
 1650 MOVE 845,60:PLOT1,-60,60:PLOT1,60,60:PLOT1,60,-60:PLOT1,-60,-60
 1660 :
 1670 VDU 4 : PRINTTAB(26,26);"" : REM "Â€"
 1680 GCOL0,10
 1690 PRINTTAB(22,22);"POSITION"
 1700 MOVE 690,232:PLOT1,280,0:PLOT1,0,50:PLOT1,-280,0:PLOT1,0,-50
 1710 PRINTTAB(15,22);"RADAR"
 1720 MOVE 445,232:PLOT1,220,0:PLOT1,0,50:PLOT1,-220,0:PLOT1,0,-40
 1730 VDU4
 1740 ENDPROC
 1750 :
 1760 DEF PROCGUAGE
 1770 VDU4:GCOL0,0:MOVE1120,120:PLOT1,70*CF,70*SF:GCOL0,1:FUEL=FUEL-(7.3/50):SF=SINFUEL:CF=COSFUEL:MOVE1120,120:PLOT1,70*CF,70*SF
 1780 ENDPROC
 1790 :
 1800 DEF PROCEND
 1810 STA=1
 1820 SOUND1,0,0,0 : SOUND2,0,0,0 : SOUND3,0,0,1
 1830 FOR J=1 TO 3
 1840   X1=X%:X2=X%:X3=X%:X4=X%:Y1=Y%:Y2=Y%:Y3=Y%:Y4=Y%
 1850   Z=-15
 1860   REPEAT
 1870     A=INT(RND(4)) : IF A=2 THEN VDU 19,0,RND(7)+7,0,0,0
 1880     PRINTTAB(X1,Y1);".";TAB(X2,Y2);".";TAB(X3,Y3);".";TAB(X4,Y4);"."
 1890     SOUND0,Z,-2,4 : Z=Z+1
 1900     FOR G=1 TO 100 : NEXT G
 1910     PRINTTAB(X1,Y1);" ";TAB(X2,Y2);" ";TAB(X3,Y3);" ";TAB(X4,Y4);" "
 1920     X1=X1-1 : X2=X2-1 : X3=X3+1:X4=X4+1:Y1=Y1-1:Y2=Y2+1:Y3=Y3-1:Y4=Y4+1
 1930   UNTIL X1=1 OR X3=1 OR X2=38 OR X4=38 OR Y1=1 OR Y2=21 OR Y3=1 OR Y4=21
 1940   FOR U=1 TO 500 : NEXT : NEXT
 1950 VDU 19,0,0,0,0,0
 1960 FOR G=1 TO 500 : NEXT G : ENDPROC
 1970 :
 1980 DEF PROCGAL
 1990 A1=0 : A=A+1 : GAL=GAL+1 : CLS : FUEL=7.8 : SOUND 1,3,52,100 : SOUND 2,3,50,100 : PROCSCRN : PROCGUAGE : ENDPROC
 2000 :
 2005 REM print score.
 2010 DEF PROCSC : GCOL0,0 : VDU 5 : MOVE 250,65 : PRINTCHR$(255);CHR$(255);CHR$(255);CHR$(255) : GCOL0,15 : MOVE30,65 : PRINTSC : VDU 4 : ENDPROC
 2020 :
 2030 DEF PROCINTRO
 2040 PRINTTAB(0,0)
 2045 R=129 : Y=131 : FOR I=1 TO 2 : PRINTSPC(8); : VDU 141,Y,65,R,83,Y,84,R,82,Y,79,135,45,Y,77,R,73,Y,78,R,69,Y,83,135:PRINT:NEXT
 2050 IF SC>HS HS=SC
 2051 PRINT : D$=CHR$(141)+" HIGH SCORE "+STR$HS
 2052 PRINTTAB(12);D$ : PRINTTAB(12);D$
 2053 D$=CHR$(141)+CHR$(134)+" LAST SCORE "+STR$SC
 2054 PRINT'TAB(11);D$ : PRINTTAB(11);D$
 2055 PRINT : PRINTTAB(6);CHR$(Y)"WRITTEN BY GRAHAM WALKDEN"
 2057 PRINT : PRINTTAB(7);CHR$(Y)"PORTED TO THE AGON LIGHT"
 2060 PRINT : PRINTTAB(11);CHR$(Y)"BY STEVE LOVEJOY"
 2070 PRINT' : FOR I=1 TO 2 : PRINTCHR$(141);SPC(8);CHR$(R);"ENGAGE CAPS LOCK NOW" : NEXT
 2110 PRINT : PRINT
 2130 PRINT'CHR$(R);"PRESS";CHR$(Y);"P";CHR$(R);"TO PLAY OR";CHR$(Y);"I";CHR$(R);"FOR INSTRUCTIONS"
 2140 A$=INKEY$(5000)
 2150 IF A$="P"THEN STA=1 : PROCINIT : A=5 : ENDPROC
 2160 CLS
 2170 D$=CHR$(141)+CHR$(Y)+"INSTRUCTIONS" : Y=1 : FOR J=1 TO 2 : PROCTELETEXT : NEXT
 2180 RESTORE 2250 : FOR J=1 TO 15 : READD$ : A$=INKEY$(0) : PROCTELETEXT : NEXT : Y=Y+1 : D$=CHR$(141)+CHR$(131)+"PRESS ANY KEY" : FOR J=1 TO 2 : PROCTELETEXT : NEXT : REM *FX21
 2190 AB=GET : CLS : Y=1 : FOR I=1 TO 7 : READD$ : PROCTELETEXT : NEXT : Y=Y+2 : D$=CHR$(141)+CHR$(131)+"PRESS ANY KEY" : FOR J=1 TO 2 : PROCTELETEXT : NEXT
 2200 AB=GET : CLS : PRINT : D$=CHR$(141)+CHR$(131)+"CONTROLS" : Y=0 : FOR J=1 TO 2 : PROCTELETEXT : NEXT
 2210 FOR J=1 TO 7 : READD$ : PROCTELETEXT : NEXT
 2220 Y1=131 : PRINT':D$=CHR$(R)+"PRESS"+CHR$(Y1)+"Z"+CHR$(R)+"TO ROTATE LEFT" : PROCTELETEXT : D$=CHR$(R)+"PRESS"+CHR$(Y1)+"X"+CHR$(R)+"TO ROTATE RIGHT":PROCTELETEXT:D$=CHR$(R)+"PRESS"+CHR$(Y1)+"SPACE"+CHR$(R)+"TO MOVE FORWARD" : PROCTELETEXT
 2230 D$=CHR$(R)+"PRESS"+CHR$(Y1)+"<ENTER>"+CHR$(R)+"TO FIRE":PROCTELETEXT:PRINT''' : D$=CHR$(141)+CHR$(131)+"PRESS"+CHR$(129)+"P"+CHR$(131)+"TO PLAY" : Y=Y+2 : FOR J=1 TO 2 : PROCTELETEXT : NEXT : REM *FX21
 2240 A$=GET$ : CLS : GOTO 2040
 2250 DATA"You are the captain of the starship","'GALAXY HOPPER' and your mission is to","search the galaxy for planets suitable","for mining. You must clear each galaxy","before you are destroyed by aliens, or","you run out of fuel."," "
 2260 DATA"To make matters worse, the star systems","have been mined by the aliens, and","the mines are highly dangerous to your","ship, though your photon torpedoes and","alien space-craft will pass through","unscathed."," "
 2270 DATA"","To play, use the keys mentioned later","to position your ship on top of the","planets ( asterisks ). As you clear","each screen, the game will become","increasingly harder, with more planets."
 2280 DATA"Pay particular attention to your fuel."
 2290 DATA""
 2300 DATA"Your space-craft is equipped with a mine","detector situated in the centre of the","control panel. When a mine is detected","in front of you, a white circle will","appear in front of the imitation space","ship on the panel."," "
 2310 END
 2320 :
 2330 DEF PROCTELETEXT : X=(39-LEND$)/2
 2340 PRINTTAB(X,Y);D$ : Y=Y+1 : ENDPROC
 2350 :
 2360 DEF PROC_HideCursor : REM Hide Cursor.
 2370 VDU 23,1,0;0;0;0;
 2380 ENDPROC
 2390 :
 2400 DEF PROC_delay(T%)
 2410 FOR k=1 TO T% : NEXT k
 2420 ENDPROC
