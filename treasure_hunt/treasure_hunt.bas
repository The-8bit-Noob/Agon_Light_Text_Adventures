   10 REM =================================================
   20 REM TREASURE HUNT BY LANCE MICKLUS, WINOOSKI, VERMONT
   30 REM COPYRIGHT SEPTEMBER 1978
   40 REM VERSION 2.1 : TRS-80/16K LEVEL II
   50 REM=================================================
   60 REM Ported to the BBC Micro in 2021 by Lee Newsome.
   70 REM Ported to the Agon Light in 2024 by Steve Lovejoy.
   80 CLS
   90 :
  100 PROCvars
  110 PROCdrawHud(HudCol%) : REM draw border etc.
  120 PROC_messageViewPort : REM set the text view port.
  130 :
  140 REM PRINT TAB(7)"T R E A S U R E   H U N T"
  150 COLOUR Text1Col% : PRINT TAB(11)"BY LANCE MICKLUS"
  160 COLOUR Text2Col% : PRINT
  170 PRINT"THE LUMUS CAVES, LOCATED IN LUMUSVILLE, VERMONT, ARE SAID TO HAVE 20 HIDDEN"
  180 PRINT"TREASURES IN THEM. FEW EXPLORE THE CAVES";
  190 PRINT"BECAUSE IT IS SAID THAT PIRATES AND     DRAGONS LIVE THERE, AND THERE ARE DEEP"
  200 PRINT"PITS WHICH MANY HAVE FALLEN INTO AND    DIED."
  210 PRINT
  220 PRINT"YOU, A SMART AND BRAVE HUMAN, AND I, AN ALERT COMPUTER, WILL EXPLORE THE CAVES"
  230 PRINT"AND TRY TO FIND THE TREASURE. I WILL BE"
  240 PRINT"YOUR EYES AND EARS, AND WILL TELL YOU IF THERE'S DANGER AHEAD."
  250 PRINT
  260 PRINT"I HOPE YOU BROUGHT A MAP. IF NOT, YOU'LL HAVE TO MAKE ONE UP AS WE EXPLORE."
  270 REM
  280 PRINT
  290 COLOUR Text1Col% : INPUT"HIT THE 'RETURN' KEY WHEN YOU'RE READYTO BEGIN";B$ : COLOUR Text2Col%
  300 CLS
  310 DIM A(425)
  320 REM
  330 REM ORISMOLOGY OF THE A ARRAY
  340 REM -------------------------
  350 REM A(0) TO A(94)     CAVE CONTENTS
  360 REM A(96) TO A(130)   TREASURE TO BE KEPT
  370 REM A(131) TO A(133)  TREASURE BEING CARRIED
  380 REM A(134) TO A(136)  ADJACENT CAVE CONNECTIONS
  390 REM A(137) TO A(421)  CAVE CONNECTION DATA ARRAY
  400 REM
  410 REM ORISMOLOGY OF NON TREASURES
  420 REM ---------------------------
  430 REM 1 = NOT USED
  440 REM 2 = PITS
  450 REM 3 = PIRATE
  460 REM 4 = DRAGON
  470 REM 5 = VENDING MACHINE
  480 REM 6 = CAVE IN >>> DANGER <<<
  490 REM 7 = ELF
  500 REM 8 = MAGICIAN'S CAVE
  510 REM 9 = INVISIBLE DOG
  520 REM
  530 REM
  540 REM     INITIALIZE & SET UP THE CAVES
  550 REM   ---------------------------------
  560 FOR I=1TO 3
  570   N=3+RND(88)
  580   IF A(N)<>0 THEN 570
  590   A(N)=2
  600 NEXT I
  610 FOR I=3 TO 9
  620   N=3+RND(88)
  630   IF A(N)<>0 THEN 620
  640   A(N)=I
  650 NEXT I
  660 FOR I=288 TO 304
  670   N=RND(94)
  680   IF A(N)<>0 THEN 670
  690   A(N)=I
  700 NEXT  I
  710 RESTORE
  720 FOR N=1 TO 23
  730   READ A$
  740 NEXT N
  750 FOR N=137 TO 421
  760   READ A(N)
  770 NEXT N
  780 W=0 : REM USER'S CAVE LOCATION
  790 M=0 : REM BECOMES A 1 WHEN CARRYING MAGIC CARPET
  800 B=200 : REM BATTERY POWER LEFT
  810 D=0 : REM BECOMES A 1 AFTER INVISIBLE DOG IS FOUND
  820 PRINT"WELCOME TO THE LUMUS CAVE WHERE YOU'LL  FIND MYSTERY, TREASURE, AND ADVENTURE."
  830 REM
  840 PRINT
  850 GOTO 920
  860 REM
  870 REM   MAIN ROUTINE
  880 REM ----------------
  890 CLS
  900 B=B-1
  910 IF B=0 THEN 4190
  920 IF W=0 THEN 3540
  930 REM
  940 REM   ::::: CHANCE SITUATIONS :::::
  950 REM
  960 IF RND(200)=200 THEN GOSUB 2290
  970 IF RND(200)=200 THEN GOSUB 2510
  980 IF D=0 AND A(W)=0 AND RND(50)=50 THEN GOSUB 2970
  990 IF A(W)=6 AND RND(5)=5 THEN 3990
 1000 IF M=1 AND RND(5)=5 THEN GOSUB 3120
 1010 REM
 1020 REM   ::::: SEE IF SPECIAL SITUATION :::::
 1030 REM
 1040 IF A(W)=2 THEN 3840
 1050 IF A(W)=4 THEN 3290
 1060 IF A(W)=5 THEN GOSUB 2170
 1070 IF A(W)=7 THEN PRINT"THERE'S A SHY LITTLE ELF IN HERE HIDING SOMETHING."
 1080 IF A(W)=8 THEN 2790
 1090 REM
 1100 REM   ::::: WHERE YOU CAN GO NEXT :::::
 1110 REM
 1120 GOSUB 3750
 1130 Z=W
 1140 GOSUB 4600
 1150 FOR N=134 TO 136
 1160   IF A(N)<0 THEN 1200
 1170   PRINT TAB(5)"CAVE ";A(N);
 1180   IF A(N)=0 PRINT " - THE ENTRANCE" : GOTO 1200
 1190   PRINT
 1200 NEXT N
 1210 REM
 1220 REM   ::::: QUIT & BATTERY TEST :::::
 1230 REM
 1240 IF B>190 THEN 1290
 1250 IF W<>0 THEN 1290
 1260 INPUT"DO YOU WANT TO EXPLORE SOME MORE";AN$
 1270 IF LEFT$(AN$,1)="N" THEN 4270
 1280 IF LEFT$(AN$,1)<>"Y" THEN 1260
 1290 IF B=50 PRINT : PRINT"I HOPE YOU BROUGHT MORE FLASH LIGHT     BATTERIES."
 1300 IF B=25 PRINT : PRINT"YOUR FLASH LIGHT IS STARTING TO DIM A   LITTLE."
 1310 IF B=8 PRINT : PRINT"YOUR FLASH LIGHT IS ALMOST OUT. BETTER  LEAVE NOW!!!"
 1320 IF A(W)=3 GOSUB 2680
 1330 REM
 1340 REM   ::::: ADJACENT CAVE'S CONTENTS :::::
 1350 REM
 1360 K=0
 1370 L=0
 1380 FOR N=134 TO 136
 1390   J=A(N)
 1400   IF J<0 THEN 1500
 1410   IF A(J)=5 THEN 1500
 1420   IF A(J)>0 AND A(J)<10 AND K=0 THEN PRINT : K=1
 1430   IFA(J)=2 AND L=0 PRINT"THERE'S PITS NEARBY. WATCH YOUR STEP." : L=1
 1440   IF A(J)=3 PRINT"CAREFUL. THERE'S A PIRATE NEAR HERE."
 1450   IF A(J)=4 PRINT"I HEAR A HUNGRY DRAGON WAITING FOR HIS  SUPPER."
 1460   IF A(J)=6 PRINT"THERE'S A SIGN HERE THAT SAYS,          >>> D A N G E R <<<"
 1470   IF A(J)=7 PRINT"SOUNDS LIKE SOMEBODY IS SINGING. MUST BEAN ELF."
 1480   IF A(J)=8 PRINT"THERE'S A STRANGE MIST IN THIS CAVE."
 1490   IF A(J)=9 PRINT"''RUFF RUFF''  I HEAR A DOG BARKING."
 1500 NEXT N
 1510 IF A(131)=0 THEN 1610
 1520 PRINT
 1530 PRINT"YOU'RE CARRYING:"
 1540 FOR N=131 TO 133
 1550   IF A(N)<100 THEN 1590
 1560   Z=A(N)
 1570   GOSUB 4520
 1580   PRINT A$,
 1590 NEXT N
 1600 PRINT
 1610 IF A(W)<100 THEN PRINT : GOTO 1840
 1620 REM
 1630 REM   ::::: TREASURE IN THIS CAVE :::::
 1640 REM
 1650 PRINT
 1660 Z=A(W)
 1670 GOSUB 4520
 1680 PRINT"THIS CAVE HAS ";A$;" IN IT."
 1690 INPUT"DO YOU WANT TO TAKE IT WITH YOU";AN$
 1700 IF LEFT$(AN$,1)="N" THEN 1840
 1710 IF LEFT$(AN$,1)<>"Y" THEN 1690
 1720 IF Z=290 GOSUB 2030
 1730 IF Z=290 THEN 1840
 1740 IF Z=291 THEN M=1
 1750 IF Z=294 GOSUB 2380
 1760 IF Z=1 THEN 1840
 1770 IF Z=295 THEN 4080
 1780 FOR N=131 TO 133
 1790   IF A(N)=0 A(N)=A(W) : A(W)=0 : n=N : N=133 : NEXT N : N=n : GOTO 1830
 1800 NEXT N
 1810 PRINT"YOU CAN'T PICK IT UP NOW. YOU'RE CARRYING TOO MUCH."
 1820 GOTO 1840
 1830 PRINT"O.K YOU'VE GOT ";A$;"."
 1840 IF A(W)=7 THEN A(W)=307
 1850 REM
 1860 REM   ::::: WHERE TO NEXT :::::
 1870 REM
 1880 COLOUR Text1Col% : INPUT"WHAT CAVE DO YOU WANT TO EXPLORE NEXT";J : COLOUR Text2Col%
 1890 FOR N=134 TO 136
 1900   IF A(N)=-1 THEN 1920
 1910   IF J=A(N) THEN W=J : n=N : N=136 : NEXT N : N=n : GOTO 890
 1920 NEXT N
 1930 IF M=1 AND J>=0 AND J<=94 THEN W=J : GOTO 890
 1940 PRINT"SORRY BUT YOU CAN'T GO THERE FROM HERE."
 1950 GOTO 1880
 1960 REM
 1970 REM
 1980 REM     SPECIAL SITUATION SUBROUTINES
 1990 REM   ---------------------------------
 2000 REM
 2010 REM   ::::: LOCK BOX :::::
 2020 REM
 2030 FOR N=131 TO 133
 2040   IF A(N)=289 n=N : N=133 : NEXT N : N=n : GOTO 2080
 2050 NEXT N
 2060 PRINT"YOU CAN'T TAKE IT. IT'S TOO HEAVY."
 2070 RETURN
 2080 PRINT"THE BOX WAS TOO HEAVY SO I OPENED IT WITH YOUR KEYS."
 2090 PRINT"INSIDE THE BOX IS A RUBY WHICH YOU CAN  TAKE."
 2100 A(W)=308
 2110 Z=308
 2120 GOSUB 4520
 2130 RETURN
 2140 REM
 2150 REM   ::::: VENDING MACHINE :::::
 2160 REM
 2170 PRINT"THERE'S A VENDING MACHINE HERE."
 2180 FOR N=131 TO 133
 2190   IF A(N)=288 THEN n=N : N=133 : NEXT N : N=n : GOTO 2220
 2200 NEXT N
 2210 RETURN
 2220 PRINT"I'LL GET SOME BATTERIES WITH ONE OF YOUR GOLD COINS."
 2230 B=200
 2240 A(W)=0
 2250 RETURN
 2260 REM
 2270 REM   ::::: EARTHQUAKE :::::
 2280 REM
 2290 FOR N=1 TO 10
 2300   Z=3+RND(88)
 2310   GOSUB 4430
 2320 NEXT N
 2330 PRINT"GUESS WHAT? WE JUST HAD AN EARTHQUAKE. BUT I'M O.K."
 2340 RETURN
 2350 REM
 2360 REM   ::::: MAGIC WAND ROUTINE :::::
 2370 REM
 2380 FOR N=131 TO 133
 2390   IF A(N)=293 n=N : N=133 : NEXT N : N=n : GOTO 2430
 2400 NEXT N
 2410 PRINT"IT'S STUCK IN THE ROCKS AND CAN'T BE PULLED OUT."
 2420 Z=1 : RETURN
 2430 PRINT"LET ME USE YOUR MAGIC WAND A SECOND."
 2440 FOR Z=0 TO 1000 : NEXT Z
 2450 PRINT"HOCUS, POCUS..."
 2460 FOR Z=0 TO 1000 : NEXT Z
 2470 A(W)=310 : Z=0
 2480 RETURN
 2490 REM
 2500 REM   ::::: BATS SHOW UP :::::
 2510 REM
 2520 PRINT"A SUPER BAT JUST FLEW INTO THEN CAVE AND PICKED YOU UP."
 2530 IF A(131)<>0 PRINT"YOU DROPPED ALL OF YOUR TREASURES."
 2540 FOR N=131 TO 133
 2550   IF A(N)=0 THEN 2570
 2560   A(0)=A(N) : A(N)=0 : Z=0 : GOSUB 4430
 2570 NEXT N
 2580 N=RND(94)
 2590 IF A(N)<>0 THEN 2580
 2600 W=N
 2610 PRINT"THE BAT JUST DROPPED YOU INTO CAVE ";W;"!!!"
 2620 INPUT"IF YOU'RE ALRIGHT HIT THE 'RETURN' KEY";B$
 2630 CLS
 2640 RETURN
 2650 REM
 2660 REM   ::::: PIRATE HERE :::::
 2670 REM
 2680 PRINT:PRINT"THERE WAS A PIRATE IN HERE."
 2690 IF A(131)<>0 THEN PRINT"HE JUST STOLE ALL OF YOUR TREASURES."
 2700 FOR N=131 TO 133
 2710   A(N)=0
 2720 NEXT N
 2730 Z=W
 2740 GOSUB 4430
 2750 PRINT"HE'S GONE NOW."
 2760 RETURN
 2770 REM   ::::: MAGICIAN ROUTINE :::::
 2780 REM
 2790 CLS
 2800 PRINT"THERE'S A MAGICIAN IN THIS CAVE. HE     SAYS HE LOST HIS MAGIC"
 2810 PRINT"BOOK. HE SAID HE'D GIVE YOU A GOLDEN    HARP IF YOU'LL TELL HIM"
 2820 PRINT"WHERE IT IS. TYPE IN THE CAVE NUMBER.   IF YOU DON'T KNOW, THEN"
 2830 PRINT"JUST TYPE 99."
 2840 PRINT
 2850 INPUT"WHERE'S HIS MAGIC BOOK";J
 2860 IF J<0 OR J>95 THEN 2910
 2870 IF A(J)<>295 THEN 4300
 2880 A(W)=309
 2890 A(J)=0
 2900 GOTO 890
 2910 Z=W
 2920 GOSUB 4430
 2930 GOTO 890
 2940 REM
 2950 ::::: INVISIBLE MAN ROUTINE :::::
 2960 REM
 2970 REM PRINT@ 320,"";
 2980 PRINT"THE INVISIBLE MAN IS HERE LOOKING FOR HIS INVISIBLE DOG."
 2990 PRINT"HE SAYS HE'LL GIVE YOU A $1000 REWARD IFYOU CAN TELL HIM WHERE HIS DOG IS."
 3000 PRINT"IF YOU DONT KNOW, GUESS."
 3010 PRINT
 3020 INPUT"WHAT CAVE IS THE INVISIBLE DOG IN";J
 3030 CLS
 3040 IF J<0 OR J>95 OR INT(J)<>J OR A(J)<>9 THEN RETURN
 3050 A(W)=305
 3060 A(J)=0
 3070 D=1
 3080 RETURN
 3090 REM
 3100 REM   ::::: MAGIC CARPET DISAPPEARS :::::
 3110 REM
 3120 FOR N=131 TO 133
 3130   IF A(N)=291 n=N : N=133 : NEXT N : N=n : GOTO 3170
 3140 NEXT N
 3150 M=0
 3160 RETURN
 3170 A(N)=0
 3180 IF N=133 THEN 3230
 3190 FOR J=N TO 132
 3200   A(J)=A(J+1)
 3210   A(J+1)=0
 3220 NEXT J
 3230 PRINT"POOF!!!"'"YOUR MAGIC CARPET JUST DISAPPEARED."
 3240 M=0
 3250 RETURN
 3260 REM
 3270 REM   ::::: DRAGON ROUTINE :::::
 3280 REM
 3290 FOR N=131 TO 133
 3300   IF A(N)=292 n=N : N=133 : NEXT N : N=n : GOTO 3330
 3310 NEXT N
 3320 GOTO 3910
 3330 PRINT"YIKES!!! THERE'S A DRAGON IN HERE."
 3340 PRINT"GIVE ME YOUR GUN, QUICK!!!"
 3350 FORN=0 TO 2000 : NEXT N
 3360 FOR N=1 TO 5
 3370   PRINT TAB(RND(25))"B A N G ! ! !"
 3380   FOR J=0 TO 500 : NEXT J
 3390 NEXT N
 3400 PRINT TAB(RND(30));"POOF!!!"
 3410 PRINT"INCREDIBLE???  THE DRAGON JUST VANISHED"
 3420 PRINT"WHEN I SHOT HIM RIGHT BETWEEN THE EYES."
 3430 PRINT : PRINT"BUT LOOK AT THIS. HE LEFT BEHIND HIS LITTLE BLACK BOOK WITH THE ADDRESSES"
 3440 PRINT"AND PHONE NUMBERS OF EVERY BEAUTIFUL"
 3450 PRINT"PRINCESS THAT LIVES IN VERMONT."
 3460 PRINT
 3470 INPUT"HIT THE 'RETURN' KEY AFTER YOU CATCH YOUR BREATH.";B$
 3480 A(W)=306
 3490 B=B+1
 3500 GOTO 890
 3510 REM
 3520 REM   ::::: AT ENTRANCE ROUTINE :::::
 3530 REM
 3540 IF A(131)=0 THEN 3610
 3550 FOR N=131 TO 133
 3560   IF A(N)=0 THEN 3600
 3570   J=96
 3580   IF A(J)<>0 THEN J=J+1 : GOTO 3580
 3590   A(J)=A(N)
 3600 NEXT N
 3610 IF A(96)=0 THEN 3670
 3620 PRINT"SO FAR, YOU'VE FOUND THESE TREASURES IN THE CAVES:"
 3630 FOR N=96 TO 130
 3640   IF A(N)=0 n=N : N=130 : NEXT N : N=n : GOTO 3670
 3650 Z=A(N) : GOSUB 4520 : PRINT A$,
 3660 NEXT N
 3670 FOR N=131 TO 133 : A(N)=0 : NEXT N
 3680 PRINT
 3690 COLOUR Text1Col% : PRINT : PRINT"YOU'RE AT THE ENTRANCE WHICH LEADS TO:" : COLOUR Text2Col%
 3700 B=B+1
 3710 GOTO 1130
 3720 REM
 3730 REM   ::::: IN CAVE ROUTINE :::::
 3740 REM
 3750 COLOUR Text1Col% : PRINT"YOU'RE IN CAVE ";W;" WHICH LEADS TO:"' : COLOUR Text2Col%
 3760 RETURN
 3770 REM
 3780 REM
 3790 REM     END OF PROGRAM ROUTINES
 3800 REM   ---------------------------
 3810 REM
 3820 REM   :::: FELL INTO A PIT ::::
 3830 REM
 3840 REM
 3850 PRINT"SORRY, BUT I TRIED TO WARN YOU."
 3860 PRINT"YOU FELL INTO A DEEP PIT AND KILLED YOURSELF!!!"
 3870 END
 3880 REM
 3890 REM   ::::: EATEN BY THE DRAGON :::::
 3900 REM
 3910 REM
 3920 PRINT"SORRY, BUT I TRIED TO TELL YOU ABOUT THAT SOUND."
 3930 PRINT"SUPPER IS NOW BEING SERVED HERE IN THE DRAGON'S CHAMBER..."
 3940 PRINT"AND YOU ARE THE SUPPER."
 3950 END
 3960 REM
 3970 REM   ::::: ROOF FELL IN :::::
 3980 REM
 3990 REM
 4000 PRINT"THE ROOF JUST FELL IN AND BURIED YOU ALIVE. TOO BAD I DON'T HAVE A SHOVEL"
 4010 PRINT"OR I'D DIG YOU OUT."
 4020 PRINT
 4030 PRINT"SEE YAH LATER."
 4040 END
 4050 REM
 4060 REM   ::::: PICKED UP MAGIC BOOK :::::
 4070 REM
 4080 CLS
 4090 REM
 4100 PRINT"I DON'T THINK YOU SHOULD HAVE DONE THAT.THE MAGICIAN WHO OWNS THAT MAGIC BOOK"
 4110 PRINT"PUT A SPELL ON IT. ANYBODY WHO TRIES TO"
 4120 PRINT"PICK IT UP TURNS INTO A FROG."
 4130 PRINT
 4140 PRINT"EXCUSE ME WHILE I LOOK FOR SOME FLIES."
 4150 END
 4160 REM
 4170 REM   ::::: BATTERIES WENT DEAD :::::
 4180 REM
 4190 REM
 4200 PRINT"NOW LOOK WHAT YOU DID. YOUR FLASH LIGHT WENT OUT AND YOU FELL INTO A PIT AND"
 4210 PRINT"KILLED YOURSELF. TOO BAD. ESPECIALLY"
 4220 PRINT"AFTER YOU WERE DOING SO WELL."
 4230 END
 4240 REM
 4250 REM   ::::: USER QUITS :::::
 4260 REM
 4270 PRINT"THE TREASURES ARE YOURS TO KEEP.        GOOD LUCK!!!"
 4280 END
 4290 REM   ::::: MAGICIAN UPSET :::::
 4300 PRINT
 4310 PRINT"YOU'VE MADE THE MAGICIAN VERY ANGRY."
 4320 PRINT"CAVE ";J;" DOESN'T HAVE A MAGIC BOOK"
 4330 PRINT"IN IT. TO PUNISH YOU, THE  MAGICIAN"
 4340 PRINT"CAST A SPELL ON YOU AND NOW YOU'RE ONLY 2 INCHES TALL. WORST YET, THE MAGICIAN"
 4350 PRINT"PUT YOU IN A SMALL JAR."
 4360 END
 4370 REM
 4380 REM     UTILITY SUBROUTINES
 4390 REM   -----------------------
 4400 REM
 4410 REM   ::::: MOVE ITEM TO NEW CAVE LOCATION :::::
 4420 REM
 4430 Y=3+RND(88)
 4440 IF A(Y)<>0 THEN 4430
 4450 IF (Y=W) OR (Y=Z) THEN 4430
 4460 A(Y)=A(Z)
 4470 A(Z)=0
 4480 RETURN
 4490 REM
 4500 REM   ::::: MAKE A$ NAME OF TREASURE :::::
 4510 REM
 4520 RESTORE
 4530 FOR X=0 TO Z-288
 4540   READ A$
 4550 NEXT X
 4560 RETURN
 4570 REM
 4580 REM   ::::: SET UP CONNECTING CAVE NUMBERS :::::
 4590 REM
 4600 FOR X=0 TO 2
 4610   A(134+X)=A(137+X+(Z*3))
 4620 NEXT X
 4630 RETURN
 4640 REM
 4650 REM 55
 4660 REM     TREASURE LIST
 4670 REM   ------------------
 4680 DATA"GOLD COINS"
 4690 DATA"SOME KEYS"
 4700 DATA"A LOCKED BOX"
 4710 DATA"A MAGIC CARPET"
 4720 DATA"AN OLD GUN"
 4730 DATA"A MAGIC WAND"
 4740 DATA"A SWORD"
 4750 DATA"MAGIC BOOK"
 4760 DATA"AN OLD CLOCK"
 4770 DATA"FURS"
 4780 DATA"A SILVER BELL"
 4790 DATA"A NECKLACE"
 4800 DATA"PEARL"
 4810 DATA"A DIAMOND"
 4820 DATA"A GOLD WATCH"
 4830 DATA"AN EMERALD"
 4840 DATA"JEWELLERY"
 4850 DATA"A $1000 BILL"
 4860 DATA"A BLACK BOOK"
 4870 DATA"SOME ELF FOOD"
 4880 DATA"A RUBY"
 4890 DATA"A GOLDEN HARP"
 4900 DATA"A SWORD"
 4910 REM
 4920 REM     DATA FOR CONNECTING CAVES
 4930 REM   -----------------------------
 4940 DATA 1,94,-1
 4950 DATA 0,2,3
 4960 DATA 1,4,5
 4970 DATA 1,6,7
 4980 DATA 2,8,9
 4990 DATA 2,10,11
 5000 DATA 3,12,13
 5010 DATA 3,14,15
 5020 DATA 4,16,17
 5030 DATA 4,18,19
 5040 DATA 5,20,21
 5050 DATA 5,22,23
 5060 DATA 6,24,25
 5070 DATA 6,26,27
 5080 DATA 7,28,29
 5090 DATA 7,30,31
 5100 DATA 8,32,33
 5110 DATA 8,34,35
 5120 DATA 9,36,37
 5130 DATA 9,38,39
 5140 DATA 10,40,41
 5150 DATA 10,42,43
 5160 DATA 11,44,45
 5170 DATA 11,46,47
 5180 DATA 12,48,49
 5190 DATA 12,50,51
 5200 DATA 13,52,53
 5210 DATA 13,54,55
 5220 DATA 14,56,57
 5230 DATA 14,58,59
 5240 DATA 15,60,61
 5250 DATA 15,62,63
 5260 DATA 16,63,64
 5270 DATA 16,34,64
 5280 DATA 17,33,65
 5290 DATA 17,36,65
 5300 DATA 18,35,66
 5310 DATA 18,38,66
 5320 DATA 19,37,67
 5330 DATA 19,40,67
 5340 DATA 20,39,68
 5350 DATA 20,42,68
 5360 DATA 21,41,69
 5370 DATA 21,44,69
 5380 DATA 22,43,70
 5390 DATA 22,46,70
 5400 DATA 23,45,71
 5410 DATA 23,48,71
 5420 DATA 24,47,72
 5430 DATA 24,50,72
 5440 DATA 25,49,73
 5450 DATA 25,52,73
 5460 DATA 26,51,74
 5470 DATA 26,54,74
 5480 DATA 27,53,75
 5490 DATA 27,56,75
 5500 DATA 28,55,76
 5510 DATA 28,58,76
 5520 DATA 29,57,77
 5530 DATA 29,60,77
 5540 DATA 30,59,78
 5550 DATA 30,62,78
 5560 DATA 31,61,79
 5570 DATA 31,32,79
 5580 DATA 32,33,80
 5590 DATA 34,35,80
 5600 DATA 36,37,81
 5610 DATA 38,39,81
 5620 DATA 40,41,82
 5630 DATA 42,43,82
 5640 DATA 44,45,83
 5650 DATA 46,47,83
 5660 DATA 48,49,84
 5670 DATA 50,51,84
 5680 DATA 52,53,85
 5690 DATA 54,55,85
 5700 DATA 56,57,86
 5710 DATA 58,59,86
 5720 DATA 60,61,87
 5730 DATA 62,63,87
 5740 DATA 64,65,88
 5750 DATA 66,67,88
 5760 DATA 68,69,89
 5770 DATA 70,71,89
 5780 DATA 72,73,90
 5790 DATA 74,75,90
 5800 DATA 76,77,91
 5810 DATA 78,79,91
 5820 DATA 80,81,92
 5830 DATA 82,83,92
 5840 DATA 84,85,93
 5850 DATA 86,87,93
 5860 DATA 88,89,94
 5870 DATA 90,91,94
 5880 DATA 0,92,93
 5890 :
 5900 DEF PROCvars
 5910 MB%=&40000 : REM Memory Bank &40000.
 5920 Black%=0
 5930 Red%=1
 5940 Green%=2
 5950 Yellow%=3
 5960 Blue%=4
 5970 Magenta%=5
 5980 Cyan%=6
 5990 LtGray%=7
 6000 DkGray%=8
 6010 IntRed%=9
 6020 IntGreen%=10
 6030 IntYellow%=11
 6040 IntBlue%=12
 6050 IntMagenta%=13
 6060 IntCyan%=14
 6070 White%=15
 6080 MODE 8
 6090 COLOUR 128+Black% : REM set background colour.
 6100 COLOUR White% : REM set foreground text colour.
 6110 PrimCol% = IntCyan%
 6120 SecCol% = IntGreen%
 6130 Text1Col% = IntCyan%
 6140 Text2Col% = White%
 6150 HudCol% = PrimCol% : REM set hud colour.
 6160 SW%=1279:SH%=1023:STW%=40:STH%=30:HSPLITY%=SH%-140 : REM SW & SH are in graphical units. STW & STH are charactors.
 6170 LT%=1:RT%=STW%-2:T1X=0:T1Y=0:T2X=0:T2Y=0 : REM margins and text position vars.
 6180 ENDPROC : REM end vars.
 6190 :
 6200 DEF PROCdrawHud(Col%)
 6210 VDU 18,0,Col%
 6220 VDU 26
 6230 MOVE 0,0 : DRAW 0,SH% : MOVE 1,0 : DRAW 1,SH% : MOVE 2,0 : DRAW 2,SH% : REM left border.
 6240 MOVE 0,0 : DRAW SW%,0 : MOVE 0,1 : DRAW SW%,1 : MOVE 0,2 : DRAW SW%,2 : REM bottom border.
 6250 MOVE SW%,0 : DRAW SW%,SH% : MOVE SW%-1,1 : DRAW SW%-1,SH% : MOVE SW%-2,2 : DRAW SW%-2,SH% : REM right border
 6260 MOVE 0,SH% : DRAW SW%,SH% : MOVE 0,SH%-1 : DRAW SW%,SH%-1 : MOVE 0,SH%-2 : DRAW SW%,SH%-2 : REM top border.
 6270 MOVE 0,HSPLITY% : DRAW SW%,HSPLITY% : MOVE 0,HSPLITY%-1 : DRAW SW%,HSPLITY%-1
 6280 MOVE 0,HSPLITY%-2 : DRAW SW%,HSPLITY%-2 : REM horizontal split 1.
 6290 COLOUR Text1Col% : PRINT''TAB(STW%/2 -12,1)"T R E A S U R E   H U N T" : PRINT TAB(STW%/2 -12,2)"=========================" : COLOUR Text2Col%
 6300 ENDPROC : REM end draw hud. PRINT TAB(7)"T R E A S U R E   H U N T"
 6310 :
 6320 DEF PROC_messageViewPort
 6330 VDU 28,LT%,STH%-2,RT%,5 : REM SET text View Port (Left,Bottom,Right,Top).
 6340 REM COLOUR 128+DkGray%. used to test VP.
 6350 CLS
 6360 ENDPROC
 6370 :
 6380 DEF PROC_resetViewPorts
 6390 VDU 26
 6400 ENDPROC
 6410 :
