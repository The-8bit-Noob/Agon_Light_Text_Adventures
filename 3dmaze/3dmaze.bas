     1 REM 3D_Maze
     2 REM by David Johnston
     3 REM Acorn Computing
     4 REM Ported to the Agon Light by Steve Lovejoy 2024.
     5 REM
     6 REM ON ERROR RUN
    10 MODE 8
    12 DIM M$(20,20)
    13 PROCoptions
    15 PROC_HideCursor
    20 IF MOV=1 THEN GOTO 50
    30 FOR B=1 TO 20 : FOR A=1 TO 20
    40 READ M$(A,B) : NEXT : NEXT
    50 A=2 : B=2 : D=3
    70 PROCdraw
    75 REM
    80 A$=GET$
    90 IF A$="/"THEN PROCforward
   100 IF A$="Z"THEN PROCanti
   110 IF A$="X"THEN PROCclock
   120 GOTO 70
   125 :
   130 DEF PROCanti
   140 IF D=1 THEN D=4 : ENDPROC
   150 D=D-1 : ENDPROC
   160 DEF PROCclock
   170 IF D=4 THEN D=1 : ENDPROC
   180 D=D+1 : ENDPROC
   187 :
   190 DEF PROCforward
   200 IF D=1 AND M$(A,B-1)="" THEN B=B-1 : ENDPROC
   210 IF D=2 AND M$(A+1,B)="" THEN A=A+1 : ENDPROC
   220 IF D=3 AND M$(A,B+1)="" THEN B=B+1 : ENDPROC
   230 IF D=4 AND M$(A-1,B)="" THEN A=A-1 : ENDPROC
   240 IF D=1 AND M$(A,B-1)="E" THEN CLS : PRINT"WELL DONE!" : A=GET : RUN
   250 IF D=2 AND M$(A+1,B)="E" THEN CLS : PRINT"WELL DONE!" : A=GET : RUN
   260 IF D=3 AND M$(A,B-1)="E" THEN CLS : PRINT"WELL DONE!" : A=GET : RUN
   270 IF D=4 AND M$(A-1,B)="E" THEN CLS : PRINT"WELL DONE!" : A=GET : RUN
   280 :
  1000 DEF PROCdraw
  1003 CLS
  1005 M=-512 : MS=512 : X=A : Y=B
  1007 REPEAT
  1010 PROCmovedraw
  1012 IF M$(X,Y)="O" THEN MOVE M,1000-M:DRAW M,M:DRAW 1000-M,M:DRAW 1000-M,1000-M:DRAW M,1000-M : GOTO 1040
  1014 IF M$(X,Y)="E" THEN MOVE M,1000-M:DRAW M,M:PLOT 85,1000-M,1000-M:PLOT 85,1000-M,M : GOTO 1040
  1020 IF BL$="O" THEN MOVE M,1000-M:DRAW M,M:DRAW M+MS,M+MS:DRAW M+MS,1000-M-MS:DRAW M,1000-M
  1025 IF BL$="E" THEN MOVE M,1000-M:DRAW M,M:PLOT 85,M+MS,1000-M-MS:PLOT 85,M+MS,M+MS
  1030 IF BR$="O" THEN MOVE 1000-M,1000-M:DRAW 1000-M,M:DRAW 1000-(M+MS),M+MS:DRAW 1000-(M+MS),1000-M-MS:DRAW 1000-M,1000-M
  1035 IF BR$="E" THEN MOVE 1000-M,1000-M:DRAW 1000-M,M:PLOT 85,1000-(M+MS),1000-M-MS:PLOT 85,1000-(M+MS),M+MS
  1040 IF BL$="O" AND POINT(M-4,1004-M)=0 THEN MOVE M,1000-M:DRAW M-(MS*2),1000-M:MOVE M,M:DRAW M-(MS*2),M
  1050 IF BR$="O" AND MS<256 AND POINT(1004-M,1004-M)=0 THEN MOVE 1000-M,1000-M:DRAW 1000-M+(MS*2),1000-M:MOVE 1000-M,M:DRAW 1000-M+(MS*2),M
  1100 UNTIL M$(X,Y)="O" OR M$(X,Y)="E" OR MS=8
  1110 ENDPROC
  1120 :
  2000 DEF PROCmovedraw
  2010 IF D=1 THEN Y=Y-1 : BL$=M$(X-1,Y) : BR$=M$(X+1,Y)
  2020 IF D=3 THEN Y=Y+1 : BL$=M$(X+1,Y) : BR$=M$(X-1,Y)
  2030 IF D=2 THEN X=X+1 : BL$=M$(X,Y-1) : BR$=M$(X,Y+1)
  2040 IF D=4 THEN X=X-1 : BL$=M$(X,Y+1) : BR$=M$(X,Y-1)
  2043 M=M+MS:MS=MS/2
  2050 ENDPROC
  2055 :
  2060 DEF PROCoptions
  2065 MOV=0
  2070 CLS : PRINT"1.PLAY GAME"
  2080 PRINT"2.MAKE MAZE";
  2090 A$=GET$
  2100 IF A$="1" THEN ENDPROC
  2110 IF A$="2" THEN GOTO 2130
  2120 GOTO 2070
  2130 CLS : PRINT"1.LOAD MAZE"
  2140 PRINT"2.SAVE MAZE"
  2150 PRINT"3.EDIT MAZE"
  2155 PRINT"4.GO TO MAIN MENU";
  2160 A$=GET$
  2170 IF A$="1"THEN MOV=1 : PROCload
  2180 IF A$="2" AND MOV=1 THEN PROCsave
  2190 IF A$="3" THEN MOV=1 : PROCedit
  2195 IF A$="4" THEN GOTO 2070
  2200 GOTO 2130
  2205 :
  2210 DEF PROCload
  2220 CLS : INPUT"PLEASE ENTER FILE-NAME ";name$
  2230 X=OPENIN(name$)
  2240 FOR B=1 TO 20 : FOR A=1 TO 20
  2250 INPUT# X,M$(A,B)
  2260 NEXT : NEXT
  2265 CLOSE# X
  2270 ENDPROC
  2275 :
  2280 DEF PROCsave
  2290 CLS : INPUT"PLEASE ENTER FILE-NAME ";name$
  2300 X=OPENOUT(name$)
  2310 FOR B=1 TO 20 : FOR A=1 TO 20
  2320 PRINT# X,M$(A,B)
  2330 NEXT : NEXT
  2335 CLOSE# X
  2340 ENDPROC
  2345 :
  2350 DEF PROCedit
  2355 WALL=1
  2360 CLS : FOR B=1 TO 20 : FOR A=1 TO 20
  2370 PRINTTAB(A,B)M$(A,B)
  2380 NEXT : NEXT
  2390 A=1 : B=1
  2402 PRINTTAB(1,25);
  2404 IF WALL=1 THEN PRINT"O"
  2406 IF WALL=2 THEN PRINT" "
  2408 IF WALL=3 THEN PRINT"E"
  2409 PRINTTAB(A,B);
  2410 A$=GET$
  2420 IF A$=":"THENPROCplace
  2430 IF A$="/"THEN WALL=WALL+1:IF WALL=4 THEN WALL=1
  2440 IF A$="Z" AND A>1 THEN A=A-1
  2450 IF A$="X" AND A<20 THEN A=A+1
  2460 IF A$="M" AND B<20 THEN B=B+1
  2470 IF A$="K" AND B>1 THEN B=B-1
  2480 IF A$="E"THEN ENDPROC
  2490 GOTO 2402
  2495 :
  2500 DEF PROCplace
  2510 PRINTTAB(A,B);
  2520 IF WALL=1 THENPRINT"O" : M$(A,B)="O"
  2530 IF WALL=2 THEN PRINT" " : M$(A,B)=""
  2540 IF WALL=3 THENPRINT"E" : M$(A,B)="E"
  2550 ENDPROC
  2560 :
  2570 DEF PROC_HideCursor
  2580 VDU 23,1,0;0;0;0;
  2590 ENDPROC
  2600 :
  5000 DATA O,O,O,O,O,O,O,O,O,O,O,O,O,O,O,O,O,O,O,O
  5010 DATA O, ,O, , , , , , ,O, , , ,O, , , , , ,O
  5020 DATA O, ,O, ,O, ,O,O, ,O, ,O, ,O, ,O,O,O, ,O
  5030 DATA O, , , ,O, , ,O, ,O, ,O,O,O, ,O, ,O, ,O
  5040 DATA O, ,O,O,O,O, , , ,O, ,O, , , , , ,O, ,O
  5050 DATA O, , , , ,O,O,O,O,O, ,O, ,O,O,O, , , ,O
  5060 DATA O,O,O,O, , , , , , , ,O, , , ,O,O,O,O,O
  5070 DATA O, ,O,O, ,O,O,O,O,O, ,O,O,O, ,O, , , ,O
  5080 DATA O, , , , , , , , ,O, , , ,O, ,O, ,O,O,O
  5090 DATA O,O,O,O,O,O, ,O, ,O,O,O,O,O, ,O, , , ,O
  5100 DATA O, , , , ,O, ,O, ,O, , , , , ,O,O,O, ,O
  5110 DATA O,O,O, ,O,O, ,O, , , ,O,O,O,O,O, , , ,O
  5120 DATA O, , , , ,O, ,O, ,O,O,O, , , , , ,O, ,O
  5130 DATA O,O, ,O,O,O, ,O,O,O,O, , ,O, ,O, ,O, ,O
  5140 DATA O, , ,O, , , , , , ,O, ,O,O, ,O,O,O,O,O
  5150 DATA O,O, , , ,O, ,O,O, ,O, ,O, , , , , , ,O
  5160 DATA O, , ,O, ,O, , , , ,O, ,O, ,O,O, ,O,O,O
  5170 DATA O, ,O,O, ,O,O,O,O,O,O, ,O, ,O, , , ,O,O
  5180 DATA O, , ,O, , , , , , , , ,O, ,O, ,O, , ,E
  5190 DATA O,O,O,O,O,O,O,O,O,O,O,O,O,O,O,O,O,O,O,O


