   10 REM ******************************
   20 REM *     ISLAND OF SECRETS      *
   30 REM *  This Version Is For The   *
   40 REM *      **Agon Light2**       *
   50 REM *   Ported From BBC Micro    *
   60 REM *     BY STEVE LOVEJOY       *
   70 REM *            2024            *
   80 REM ******************************
   90 REM
  100 REM CREDITS:
  110 REM Island Of Secrets was originally
  120 REM written for Usborne Books back in 1984.
  130 REM
  140 REM It is intended to be played with the aid of the book.
  150 REM The book has maps and other imformation that is needed to play this game.
  160 REM Download the book from the below link.
  170 REM Link: https://usborne.com/gb/books/computer-and-coding-books
  180 :
  190 CLEAR
  200 PROC_load_vars
  210 PROC_load_chars
  220 PROC_initialise
  230 :
  240 D=R : IF R=20 THEN D=FNR(80)
  250 GOSUB 1020
  260 PROC_resetViewPorts
  270 PROC_drawHud(PrimCol%)
  280 PROC_drawIOS(LT%,1,PrimCol%,SecCol%)
  290 :
  300 COLOUR SecCol% : PRINT TAB(11,1) "FOOD :";
  310 COLOUR PrimCol% : PRINT; F
  320 COLOUR SecCol% : PRINT TAB(11,2) "DRINK:";
  330 COLOUR PrimCol% : PRINT; G
  340 COLOUR SecCol% : PRINT TAB(19,1) "POWER :";
  350 COLOUR PrimCol% : PRINT; INT(Y)
  360 COLOUR SecCol% : PRINT TAB(19,2) "WISDOM:"; : IF X < 100 PRINT " ";
  370 COLOUR PrimCol% : PRINT; X
  380 COLOUR SecCol% : PRINT TAB(30,1) "TIME:";
  390 COLOUR PrimCol% : PRINT; L
  400 COLOUR Text1Col%
  410 PROC_messageViewPort
  420 PRINT "YOU ARE "; I$(VAL(LEFT$(A$,1))); " "; : GOSUB 1090 : N=0
  430 FOR I = 1 TO C4
  440   C=0 : READ Y$
  450   IF L(I)=R AND F(I)<1 THEN C=1
  460   IF N>0 AND C=1 THEN A$=A$+ ","
  470   IF C=1 THEN A$=A$ + " " + Y$ : N=N+1
  480 NEXT I
  490 IF N>0 THEN A$= "*YOU SEE" + A$ : GOSUB 1090
  500 PRINT F$ : REM print message.
  510 PRINT "WHAT WILL YOU DO" : COLOUR Text2Col%
  520 INPUT E$
  530 C$= "" : X$= "" : A=0 : O=52 : LI=LEN(E$)
  540 FOR I = 1 TO LI
  550   IF MID$(E$,I,1)= " " AND C$= "" THEN C$=LEFT$(E$,I-1)
  560   IF MID$(E$,I+1,1)<> " " AND C$> "" THEN X$=RIGHT$(E$,LI-I) : I=LI
  570 NEXT I
  580 IF X$= "" THEN C$=E$
  590 IF LEN(C$)<3 THEN C$=C$+ "???"
  600 FOR I =1 TO V
  610   IF LEFT$(C$,3)=MID$(V$,3*(I-1)+1,3) THEN A=I
  620 NEXT I
  630 GOSUB 1130
  640 B$= "" : IF A=0 THEN A=V+1
  650 IF X$= "???" THEN F$= "MOST ACTIONS NEED TWO WORDS"
  660 IF A>V OR O=52 THEN F$=W$+C$+ " " +X$
  670 IF A>V AND O=52 THEN F$= "WHAT!"
  680 L=L-1 : Y=FNS(Z)
  690 B$=STR$(O)+STR$(L(O))+STR$(F(O))+STR$(R)
  700 ON INT(A/10)+1 GOSUB 960,970,980,990,1000,1000,1010
  710 IF R=61 THEN X=X-FNR(2)+1
  720 IF R=14 AND FNR(3)=1 THEN Y=Y-1 : F$= "YOU ARE BITTEN"
  730 IF F(36)<1 AND -R<>F(22) THEN F(36)=F(36)+1 : L(36)=R : Y=Y-1
  740 IF R<>L(16) AND L(16)>0 THEN L(16)=1+FNR(4)
  750 IF R<>L(39) THEN L(39)=10*(FNR(5)-1)+7+FNR(3)
  760 IF R=L(39) AND R<>L(43) AND F(13)>-1 THEN Y=Y-2 : X=X-2
  770 IF R<78 THEN L(32)=76+FNR(2)
  780 IF R=33 OR R=57 OR R=73 AND FNR(2)=1 THEN L(25)=R
  790 IF R=L(32)AND FNR(2)=1 AND F(32)=0 THEN GOSUB 1680
  800 IF R=19 AND Y<70 AND F(43)=0 AND FNR(4)=1 THEN F$= "PUSHED INTO THE PIT" : F(W)=1
  810 IF R<>L(41) THEN L(41)=21+(FNR(3)*10)+FNR(2)
  820 IF R=L(41) THEN F(41)=F(41)-1 : IF F(41)<-4 THEN GOSUB 1600
  830 IF F(43)=0 THEN L(43)=R
  840 IF L(43)<18 AND R<>9 AND R<>10 AND F(W-2)<1 THEN GOSUB 1700
  850 IF R=18 THEN Y=Y-1
  860 IF Y<50 THEN O=FNR(9) : GOSUB 1900 : IF L(O)=R THEN F$= "YOU DROP SOMETHING"
  870 IF L<900 AND R=23 AND F(36)>0 AND FNR(3)=3 THEN GOSUB 1730
  880 IF R=47 AND F(8)>0 THEN F$=F$ + " YOU CAN GO NO FURTHER"
  890 IF F(8)+F(11)+F(13)=-3 THEN F(W)=1 : GOSUB 3170
  900 IF F(W)=0 AND L>0 AND Y>1 AND X>1 THEN GOTO 240
  910 IF L<1 OR Y<1 THEN F$= "YOU HAVE FAILED, THE EVIL ONE SUCCEEDS"
  920 PRINT 'F$ : PRINT "YOUR FINAL SCORE="; INT(X+Y+(ABS(L/7*(L<640))))
  930 PRINT : PRINT : PRINT "GAME OVER"
  940 END : REM end game
  950 :
  960 ON A GOSUB 1180,1180,1180,1180,1180,1450,1450,1760,1900 : RETURN
  970 ON A-9 GOSUB 1910,2000,2040,2080,2100,1450,2130,2130,2130,2130 : RETURN
  980 ON A-19 GOSUB 2190,2190,2190,2190,2280,2470,2580,2640,2640,1450 : RETURN
  990 ON A-29 GOSUB 2870,2870,2670,2670,2700,2720,2770,2770,2840,2910 : RETURN
 1000 ON A-39 GOSUB 2970,2970,3090,1010
 1010 RETURN
 1020 RESTORE
 1030 FOR I = 1 TO D : READ A$
 1040 NEXT I
 1050 D$=RIGHT$(A$,4) : A$=LEFT$(A$,LEN(A$)-4)
 1060 IF R=39 THEN D$=MID$( "101110100" ,FNR(5),4)
 1070 IF R=20 THEN D$= "1110"
 1080 GOSUB 3150 : RETURN
 1090 FOR I = 2 TO LEN(A$) : E$=MID$(A$,I,1)
 1100   PRINT E$; : IF E$= " " AND FNP(Z)>Z THEN PRINT
 1110 NEXT I
 1120 PRINT ". " : A$= "" : RETURN
 1130 IF LEN(X$)<3 THEN X$=X$+ "???"
 1140 FOR I = 1 TO W
 1150   IF LEFT$(X$,3)=MID$(Z$,3*(I-1)+1,3) THEN O=I
 1160 NEXT I : IF O=0 THEN O=52
 1170 RETURN
 1180 D=0 : C=0 : IF O=52 THEN D=A
 1190 IF O>C4 AND O<W THEN D=O-C4
 1200 IF B$= "500012" OR B$= "500053" OR B$= "500045" THEN D=4
 1210 IF B$= "500070" OR B$= "500037" OR B$= "510011" OR B$= "510041" THEN D=1
 1220 IF B$= "510043" OR B$= "490066" OR B$= "490051" THEN D=1
 1230 IF B$= "510060" OR B$= "480056" THEN D=2
 1240 IF B$= "510044" OR B$= "510052" THEN D=3
 1250 IF B$= "490051" AND F(29)=0 THEN GOSUB 2480 : RETURN
 1260 IF R=L(39)AND (X+Y<180 OR R=10)THEN F$=W$ + "LEAVE!" : RETURN
 1270 IF R=L(32)AND F(32)<1 AND D=3 THEN F$= "HE WILL NOT LET YOU PAST" : RETURN
 1280 IF R=47 AND F(44)=0 THEN F$= "THE ROCKS MOVE TO PREVENT YOU" : RETURN
 1290 IF R=28 AND F(7)<>1 THEN F$= "THE ARMS HOLD YOU FAST" : RETURN
 1300 IF R=45 AND F(40)=0 AND D=4 THEN F$= "HISSSS!" : RETURN
 1310 IF R=25 AND F(16)+L(16)<>-1 AND D=3 THEN F$= "TOO STEEP TO CLIMB" : RETURN
 1320 IF R=51 AND D=3 THEN F$= "THE DOOR IS BARRED!" : RETURN
 1330 IF D>0 THEN IF MID$(D$,D,1)= "0" THEN R=R+VAL(MID$("-10+10+01-01" ,D*3-2,3)) : C=1
 1340 F$= "OK"
 1350 IF D<1 OR C=0 THEN F$=W$+ "GO THAT WAY"
 1360 IF R=33 AND L(16)=0 THEN L(16)=FNR(4) : F(16)=0 : F$= "THE BEAST RUNS AWAY!"
 1370 IF R<>L(25)OR O<>25 THEN RETURN
 1380 F$= "" : A$= "#YOU BOARD THE CRAFT "
 1390 IF X<60 THEN A$=A$+S$
 1400 A$=A$+T$
 1410 GOSUB 3110 : GOSUB 3130 : GOSUB 3130
 1420 IF X<60 THEN A$= "#TO SERVE OMEGAN FOREVER!" : F(W)=1
 1430 IF X>59 THEN A$= "#THE BOAT SKIMS THE DARK SILENT WATERS" : R=57
 1440 GOSUB 3120 : GOSUB 3130 : GOSUB 3130 : RETURN
 1450 IF((F(O)>0 AND F(O)<9)OR L(O)<>R)AND O<=C3 THEN F$= "WHAT " + X$ + "?" : RETURN
 1460 IF B$= "3450050" THEN Y=Y-8 : X=X-5 : F$= "THEY ARE CURSED" : RETURN
 1470 IF B$= "3810010" THEN GOSUB 1740
 1480 IF(A=15 AND O<>20 AND O<>1) OR (A=29 AND O<>16)OR O>C3 THEN F$=W$ + C$ + " " + X$ : RETURN
 1490 IF L(O)=R AND (F(O)<1 OR F(O)=9) AND O<C3 THEN L(O)=0 : A=-1
 1500 IF O=16 AND L(10)<>0 THEN L(O)=R : F$= "IT ESCAPED" : A=0
 1510 IF O>C1 AND O<C2 THEN F=F+2 : A=-1
 1520 IF O>=C2 AND O<=C3 THEN G=G+2 : A=-1
 1530 IF O>C1 AND O<C3 THEN L(O)=-81
 1540 IF A=-1 THEN F$= "TAKEN" : X=X+4 : E=E+1 : IF F(O)>1 THEN F(O)=0
 1550 IF B$<> "246046" OR L(11)=0 THEN RETURN
 1560 F$=U$ : L(O)=R : IF FNR(3)<3 THEN RETURN
 1570 A$= "#" +U$+R$
 1580 R=63+FNR(6) : L(16)=1 : F$= ""
 1590 GOSUB 3110 : RETURN
 1600 GOSUB 3140 : F$= "" : A$= "#THE LOGMEN " +M$
 1610 F(41)=0 : Y=Y-4 : X=X-4
 1620 IF R<34 THEN A$=A$+ "THROW YOU IN THE WATER" : R=32
 1630 IF R>33 THEN A$=A$+ "TIE YOU UP IN A STOREROOM" : R=51
 1640 GOSUB 3120 : GOSUB 3130
 1650 FOR I =3 TO 4
 1660   IF L(I)=0 THEN L(I)=42
 1670 NEXT I : RETURN
 1680 A$= "*THE SWAMPMAN TELLS HIS TALE"
 1690 GOSUB 3110 : F(32)=-1 : RETURN
 1700 F$= "MEDIAN CAN DISABLE THE EQUIPMENT"
 1710 IF L(8)=0 THEN F$=F$+ " AND ASKS YOU FOR THE PEBBLE YOU CARRY"
 1720 RETURN
 1730 F(36)=-(FNR(4)+6) : F$= "A STORM BREAKS OVERHEAD!" : RETURN
 1740 FOR K=1 TO 30 : GOSUB 3140 : PRINT "////LIGHTNING FLASHES!" : NEXT K
 1750 L(39)=R : Y=Y-8 : X=X-2 : RETURN
 1760 IF (O<>24 AND L(O)>0) OR O=52 THEN F$= "YOU DON'T HAVE THE "+X$ : RETURN
 1770 PRINT "GIVE THE "; X$; " TO WHOM"; : INPUT X$
 1780 Q=O : GOSUB 1130 : N=O : O=Q
 1790 IF R<>L(N) THEN F$= "THE " +X$+ " IS NOT HERE" : RETURN
 1800 IF B$= "10045" AND N=40 THEN LET L(O)=81 : F(40)=1 : F$= "THE SNAKE UNCURLS"
 1810 IF B$= "2413075" AND N=30 AND G>1 THEN F(11)=0 : F$= "HE OFFERS HIS STAFF" : G=G-1
 1820 B$=LEFT$(B$,3):F$= "IT IS REFUSED"
 1830 IF B$= "300" AND N=42 THEN X=X+10 : L(O)=81
 1840 IF B$= "120" AND N=42 THEN X=X+10 : L(O)=81
 1850 IF B$= "40-" AND N=32 THEN F(N)=1 : L(O)=81
 1860 IF LEFT$(B$,2)= "80" AND N=43 THEN L(O)=81 : GOSUB 1930
 1870 IF L(O)=81 OR (O=24 AND L(11)>0 AND G>0) THEN F$= "IT IS ACCEPTED"
 1880 IF N=41 THEN L(O)=51 : F$= "IT IS TAKEN"
 1890 RETURN
 1900 IF O=4 AND L(O)= 0 THEN L(O)=81 : X=X-1 : F$= "IT BREAKS!" : RETURN
 1910 IF L(O)=0 AND O<=C1 THEN L(O)=R : F$= "DONE" : E=E-1
 1920 RETURN
 1930 A$= "*HE TAKES IT " : IF R<>8 THEN A$=A$+ "RUNS DOWN THE CORRIDOR,"
 1940 GOSUB 3110 : A$= "*AND CASTS IT INTO THE CHEMICAL VATS, PURIFYING THEM WITH"
 1950 A$=A$+ " A CLEAR BLUE LIGHT REACHING FAR INTO THE LAKES AND RIVERS BEYOND"
 1960 F(8)=-1 : GOSUB 3120 : GOSUB 3130 : GOSUB 3130 : RETURN
 1970 IF L(I)<>0 AND I<C1 THEN I=I+1 : GOTO 1970
 1980 IF L(I)=0 THEN L(I)=R : F(I)=0 : GOSUB 1910 : F$= "YOU DROP SOMETHING"
 1990 RETURN
 2000 IF(O<C1 OR O>C3) AND X$<> "???" THEN F$=W$+C$+ " " +X$:X=X-1 : RETURN
 2010 F$= "YOU HAVE NO FOOD" : IF F>0 THEN F=F-1 : Y=Y+10 : F$= "OK"
 2020 IF O=3 THEN X=X-5:Y=Y-2 : F$= "THEY MAKE YOU VERY ILL!"
 2030 RETURN
 2040 IF O=31 THEN GOSUB 2750 : RETURN
 2050 IF X$<> "???" AND (O<21 OR O>C3) THEN F$=W$+C$+ " " +X$ : X=X-1 : RETURN
 2060 F$= "YOU HAVE NO DRINK" : IF G>0 THEN G=G-1 : Y=Y+7 : F$= "OK"
 2070 RETURN
 2080 IF LEFT$(B$,4)= "1600" THEN F(O)=-1 : F$= "IT ALLOWS YOU TO RIDE"
 2090 RETURN
 2100 IF B$= "2644044" THEN F$= "CHEST OPEN" : F(6)=9 : F(5)=9 : F(15)=9
 2110 IF B$= "2951151" THEN F$= "THE TRAPDOOR CREAKS" : F(29)=0 : X=X+3
 2120 RETURN
 2130 Y=Y-2 : IF B$= "3577077" AND L(9)=0 THEN F(23)=0 : L(23)=R
 2140 IF V>15 AND V<19 AND (L(9)=0 OR L(15)=0) THEN F$= "OK"
 2150 IF B$= "1258158" OR B$= "2758158" AND L(15)=0 THEN F(12)=0 : F(27)=0 : F$= "CRACK!"
 2160 IF LEFT$(B$,4)= "1100" AND R=10 THEN GOSUB 2350
 2170 IF A=18 AND (O>29 AND O<34) OR (O>38 AND O<44) OR O=16 THEN GOSUB 2270
 2180 RETURN
 2190 Y=Y-2 : X=X-2 : IF R<>L(O) AND L(O)<>0 THEN RETURN
 2200 IF O=39 THEN F$= "HE LAUGHS DANGEROUSLY"
 2210 IF O=32 THEN F$= "THE SWAMPMAN IS UNMOVED"
 2220 IF O=33 THEN F$=W$+ "TOUCH HER!" : L(3)=81
 2230 IF O=41 THEN F$= "THEY THINK THAT'S FUNNY! "
 2240 IF R=46 GOSUB 1570
 2250 IF LEFT$(B$,4)= "1400" AND R=L(39) THEN GOSUB 2350
 2260 Y=Y-8 : X=X-5 : RETURN
 2270 IF L(9)>0 THEN RETURN
 2280 Y=Y-12 : X=X-10 : F$= "THAT WOULD BE UNWISE!"
 2290 IF R<>L(O) THEN RETURN
 2300 F(W)=1 : A$= "#THUNDER SPLITS THE SKY! " : F$=""
 2310 A$=A$+ "IT IS THE TRIUMPHANT VOICE OF OMEGAN." : GOSUB 3110
 2320 A$= "#WELL DONE ALPHAN! THE MEANS BECOME THE END.. "
 2330 A$=A$+ "I CLAIM YOU AS MY OWN! HA HA HAH!" : GOSUB 3120
 2340 GOSUB 3130 : X=0 : L=0 : Y=0 : RETURN
 2350 GOSUB 3140 : ON O-10 GOSUB 2380,2430,2430,2430
 2360 X=X+10:L(O)=81 : F(O)=-1 : GOSUB 1090 : GOSUB 3130 : GOSUB 3130
 2370 RETURN
 2380 A$= "#IT SHATTERS RELEASING A DAZZLING RAINBOW OF COLOURS!"
 2390 IF L(2)<>R THEN RETURN
 2400 A$=A$+ "THE EGG HATCHES INTO A BABY DACTYL " +O$
 2410 L(39)=81 : L(2)=81 : F(2)=-1 : Y=Y+40
 2420 RETURN
 2430 IF L(13)<>R THEN RETURN
 2440 A$= "*THE COAL BURNS WITH A WARM RED FLAME" : F(13)=-1
 2450 IF R=10 AND R=L(39) THEN A$=A$+ " WHICH DISSOLVES OMEGAN'S CLOAK" : Y=Y+20
 2460 RETURN
 2470 IF R<>51 OR F(29)>0 THEN F$=W$+C$+ " HERE" : X=X+1
 2480 X=X-1 : R=FNR(5) : GOSUB 3140 : PRINT "SWIMMING IN THE POISONOUS WATERS"
 2490 J=0 : B$="" : F$= "YOU SURFACE" : PRINT "YOUR STRENGTH = "; INT(Y)
 2500 FOR I=1 TO R
 2510   IF Y<15 THEN PRINT "YOU ARE VERY WEAK"
 2520   PRINT "WHICH WAY"; : INPUT X$ : X$=LEFT$(X$,1) : B$=B$+X$ : NEXT I
 2530 FOR I=1 TO R
 2540   Y=FNS(Z)-3 : IF MID$(B$,I,1)="N" THEN J=J+1
 2550 NEXT I  : IF R/2>J AND Y>1 THEN 2480
 2560 IF Y<2 THEN F$= "YOU GOT LOST AND DROWNED"
 2570 R=30+FNR(3) : RETURN
 2580 IF F(36)>-1 THEN RETURN
 2590 GOSUB 3140 : PRINT "YOU CAN RUN TO SHELTER IN:" : PRINT "1) GRANDPA'S SHACK"
 2600 PRINT "2) CAVE OF SNELM"'"3) LOG CABIN" : PRINT "CHOOSE FROM 1-3" : INPUT A$
 2610 IF A$>"0" AND A$<"4" THEN R=ASC(MID$("A >",VAL(A$),1))-21 : F(22)=-R
 2620 PRINT "YOU RUN BLINDLY THROUGH THE STORM" : F$= "YOU REACH SHELTER"
 2630 GOSUB 3130 : RETURN
 2640 IF B$= "3075075" OR B$= "3371071" THEN F$= "HOW WILL YOU DO THAT"
 2650 IF B$= "3371071" AND A=28 THEN F(3)=0 : F$= "SHE NODS SLOWLY" : X=X+5
 2660 RETURN
 2670 F$= "EXAMINE THE BOOK FOR CLUES"
 2680 IF LEFT$(B$,3)= "600" THEN F$=L$
 2690 RETURN
 2700 IF B$= "40041" THEN F(4)=-1 : F$= "FILLED"
 2710 RETURN
 2720 F$=X$ : IF X$=H$ AND R=47 AND F(8)=0THEN F(44)=1 : F$=J$
 2730 IF X$<>P$ OR R<>L(42) OR L(3)<81 OR L(12)<81 THEN RETURN
 2740 F$= "HE EATS THE FLOWERS - AND CHANGES" : F(42)=1 : F(43)=0 : RETURN
 2750 IF F(4)+L(4)<>-1 THEN F$= "YOU DON'T HAVE " +X$ : RETURN
 2760 GOSUB 3140 : PRINT "YOU TASTE A DROP AND..." : GOSUB 3130 : F$= "*OUCH!" : Y=Y-4 : X=X-7
 2770 GOSUB 2640 : FOR I=1 TO ABS(F(36))+3
 2780   L=L-1 : IF Y<100 OR -R=F(22) THEN Y=Y+1
 2790   PRINT "TIME PASSES..." : GOSUB 3130
 2800 NEXT I
 2810 IF L>100 OR F(36)<1 THEN X=X+2 : F(36)=1
 2820 IF A=37 OR A=36 THEN F$="OK"
 2830 RETURN
 2840 IF R=L(25)THEN F$= "THE BOATMAN WAVES BACK"
 2850 IF LEFT$(B$,3)= "700" THEN F(7)=1 : F$=N$ : X=X+8
 2860 RETURN
 2870 F$= "A-DUB-DUB" : IF LEFT$(B$,4)<>"2815" THEN RETURN
 2880 IF F(O)=1 THEN F(O)=0 : F$=K$ : RETURN
 2890 IF L(5)=0 THEN F(8)=0 : GOSUB 1450 : F$= "THE STONE UTTERS " +H$
 2900 RETURN
 2910 GOSUB 3140 : PRINT " INFO - ITEMS CARRIED" : GOSUB 3150
 2920 PRINT G$; TAB(0); " FOOD="; F; TAB(23); "DRINK="; G : PRINT G$; : F$= "OK"
 2930 FORI=1 TO C4
 2940   READ Y$ : IF L(I)=0 THEN PRINT Y$
 2950 NEXT I
 2960 PRINT G$; : GOSUB 3100 : RETURN
 2970 C$="LOAD" : IF A=41 THEN C$= "SAVE"
 2980 PRINT "PREPARE TO "; C$ : GOSUB 3100
 2990 IF A=40 THEN FL=OPENIN "ISDATA"
 3000 IF A=41 THEN FL=OPENOUT "ISDATA"
 3010 IF A=41 THEN F(50)=R:F(49)=Y:F(48)=X:F(47)=F:F(46)=G:F(45)=L
 3020 FOR I=1 TO W
 3030   IF A=40 THEN INPUT #FL,L(I),F(I)
 3040   IF A=41 THEN PRINT #FL,L(I),F(I)
 3050 NEXT I
 3060 CLOSE#FL
 3070 IF A=40 THEN R=F(50):Y=F(49):X=F(48):F=F(47):G=F(46):L=F(45)
 3080 F$="OK" : RETURN
 3090 F(W)=-1 : F$= "YOU RELINQUISH YOUR QUEST." : L=1 : RETURN
 3100 INPUT"PRESS ENTER";A$ : RETURN
 3110 GOSUB 3140
 3120 GOSUB 1090 : GOSUB 3130 : RETURN
 3130 FOR D= 1 TO 2000 : NEXT D : RETURN
 3140 CLS : RETURN
 3150 RESTORE : FOR I = 1 TO 80 : READ TP$ : NEXT I
 3160 RETURN
 3170 A$= "*THE WORLD LIVES WITH NEW HOPE!" : GOSUB 3120
 3180 F$= "YOUR QUEST IS OVER" : REM  RETURN
 3190 :
 3200 DEF PROC_initialise
 3210 :
 3220 LR=2860 : Z=38 : Z=INT(Z*.8)
 3230 V=42 : W=51 : C4=43
 3240 DIM I$(7)
 3250 DIM L(52),F(52)
 3260 DATA "4THE FURTHEST DEPTHS OF THE FOREST1001"
 3270 DATA "4THE DEPTHS OF THE MUTANT FOREST1000"
 3280 DATA "7A PATH OUT OF THE OVERGROWN DEPTHS1000"
 3290 DATA "6A CARNIVOROUS TREE1000"
 3300 DATA "4A CORRAL BENEATH THE CRIMSON CANYON1110"
 3310 DATA "7THE TOP OF A STEEP CLIFF1011"
 3320 DATA "4THE MARSH FACTORY1001"
 3330 DATA "4THESLUDGE FERMENTATION VATS1110"
 3340 DATA "7THE UPPERMOST BATTLEMENTS1001"
 3350 DATA "4OMEGAN'S SANCTUM1110"
 3360 DATA "4SNELM'S LAIR0001"
 3370 DATA "2A DARK CAVE0000"
 3380 DATA "1BROKEN BRANCHES0100"
 3390 DATA "1A THICKET OF BITING BUSHES0000"
 3400 DATA "1A HUGE GLASSY STONE1110"
 3410 DATA "7THE EDGE OF CRIMSON CANYON0011"
 3420 DATA "4THE CLONE FACTORY0101"
 3430 DATA "4A CORRIDOR OF CLONE STORAGE CASKS1100"
 3440 DATA "7EDGE OF THE WELL0000"
 3450 DATA "4THE ROOM OF SECRET VISIONS1110"
 3460 DATA "4SNELM'S INNER CHAMBER0111"
 3470 DATA "3THE SOUTHERN EDGE OF THE FOREST0101"
 3480 DATA "7A LEAFY PATH1000"
 3490 DATA "3A FORK IN THE PATH0100"
 3500 DATA "7AN APPARENTLY UNCLIMBABLE ROCKY PATH1100"
 3510 DATA "7A LEDGE ATOP THE CRIMSON CANYON0010"
 3520 DATA "4A TALL ENTRANCE CHAMBER1101"
 3530 DATA "4A LOW PASSAGE WITH ARMS REACHING FROM THE WALLS1010"
 3540 DATA "7THE APPROACH TO THE WELL OF DESPAIR0001"
 3550 DATA "4A DIM CORRIDOR DEEP IN THE CASTLE1010"
 3560 DATA "4THE STAGNANT WATERS OF THE CRAWLING CREEK1001"
 3570 DATA "4A SHALLOW POOL OFF THE CREEK1100"
 3580 DATA "7A LOG PIER, JUTTING OUT OVER THE CREEK0000"
 3590 DATA "4A STRETCH OF FEATURELESS DUNES1100"
 3600 DATA "1A GROUP OF TALL TREES1010"
 3610 DATA "7A NARROW LEDGE AT THE SUMMIT OF THE CANYON0011"
 3620 DATA "2A MONSTROUS PORTAL IN THE CASTLE WALL0011"
 3630 DATA "4A CHAMBER INCHES DEEP WITH DUST0001"
 3640 DATA "4HERE1111"
 3650 DATA "2A CARVED ARCHWAY0010"
 3660 DATA "4A SMALL HUT IN THE LOG SETTLEMENT0111"
 3670 DATA "1A HUGE SPLIT-LOG TABLE1001"
 3680 DATA "4THE PORCH OF THE LOGMEN'S CABIN0110"
 3690 DATA "4GRANDPA'S SHACK1101"
 3700 DATA "3A CLEARING IN THE TREES BY A RICKETY SHACK0010"
 3710 DATA "4THE NEST OF A HUGE DACTYL0111"
 3720 DATA "6THE CASTLE OF DARK SECRETS BY TWO HUGE STONES0011"
 3730 DATA "4A ROOM LITTERED WITH BONES0111"
 3740 DATA "4THE CELL OF WHISPERED SECRETS0111"
 3750 DATA "4THE LIBRARY OF WRITTEN SECRETS0111"
 3760 DATA "4A REFUSE STREWN STOREROOM 1111"
 3770 DATA "4THE LOGMEN'S HALL0000"
 3780 DATA "5A LOG BUILDING1000"
 3790 DATA "7A RUTTED HILLSIDE1100"
 3800 DATA "7A WINDSWEPT PLAIN AMONGST STONE MEGALITHS0100"
 3810 DATA "7THE STEPS OF AN ANCIENT PYRAMID1010"
 3820 DATA "7THE ISLAND OF SECRETS0111"
 3830 DATA "1A BROKEN MARBLE COLUMN1001"
 3840 DATA "7AN EXPANSE OF CRACKED, BAKED EARTH1100"
 3850 DATA "4A DESERTED ADOBE HUT1010"
 3860 DATA "4A LIVID GROWTH OF MAD ORCHIDS1011"
 3870 DATA "4A CORNER STREWN WITH BROKEN CHAIRS0111"
 3880 DATA "7THE BRIDGE NEAR TO A LOG SETTLEMENT0011"
 3890 DATA "1A CRUMBLING MASS OF PETRIFIED TREES1011"
 3900 DATA "3THE EDGE OF THE PYRAMID1101"
 3910 DATA "7THE ROOF OF THE ANCIENT PYRAMID0100"
 3920 DATA "3AN IMPASSABLE SPLIT IN THE PYRAMID1110"
 3930 DATA "7A BARREN BLASTED WASTELAND0001"
 3940 DATA "4AN EXPANSE OF BLEAK, BURNT LAND1100"
 3950 DATA "5A DILAPIDATED ADOBE HUT0110"
 3960 DATA "4THE HEART OF THE LILIES0101"
 3970 DATA "4THE MIDST OF THE LILIES1100"
 3980 DATA "3A RIVER'S EDGE BY A LOG BRIDGE0100"
 3990 DATA "3A PETRIFIED VILLAGE BY A RIVER CROWDED WITH LILIES0100"
 4000 DATA "4THE REMAINS OF A VILLAGE1100"
 4010 DATA "3THE ENTRANCE TO A PETRIFIED VILLAGE1100"
 4020 DATA "4A SWAMP MATTED WITH FIBROUS ROOTS1100"
 4030 DATA "2A VILLAGE OF HOLLOW STUMPS DEFYING THE SWAMP0100"
 4040 DATA "4A TUNNEL INTO ONE OF THE TREE STUMPS1100"
 4050 DATA "4A HOLLOW CHAMBER MANY METRES IN DIAMETER1110"
 4060 DATA "A SHINY APPLE"
 4070 DATA "A FOSSILISED EGG"
 4080 DATA "A LILY FLOWER"
 4090 DATA "AN EARTHENWARE JUG"
 4100 DATA "A DIRTY OLD RAG"
 4110 DATA "A RAGGED PARCHMENT"
 4120 DATA "A FLICKERING TORCH"
 4130 DATA "A GLISTENING APPLE"
 4140 DATA "A WOODMAN'S AXE"
 4150 DATA "A COIL OF ROPE"
 4160 DATA "A RUGGED STAFF"
 4170 DATA "A CHIP OF MARBLE"
 4180 DATA "A POLISHED COAL"
 4190 DATA "A PIECE OF FLINT"
 4200 DATA "A GEOLOGIST'S HAMMER"
 4210 DATA "A WILD CANYON BEAST"
 4220 DATA "A GRAIN LOAF"
 4230 DATA "A JUICY MELON"
 4240 DATA "SOME BISCUITS"
 4250 DATA "A GROWTH OF MUSHROOMS"
 4260 DATA "A BOTTLE OF WATER"
 4270 DATA "A FLAGON OF WINE"
 4280 DATA "A FLOWING SAP"
 4290 DATA "A SPARKLING FRESHWATER SPRING"
 4300 DATA "THE BOATMAN"
 4310 DATA "A STRAPPED OAK CHEST"
 4320 DATA "A FRACTURE IN THE COLUMN"
 4330 DATA "A MOUTH-LIKE OPENING"
 4340 DATA "AN OPEN TRAPDOOR"
 4350 DATA "A PARCHED, DESSICATED VILLAGER"
 4360 DATA "A STILL OF BUBBLING GREEN LIQUOR"
 4370 DATA "A TOUGH SKINNED SWAMPMAN"
 4380 DATA "THE SAGE OF THE LILIES"
 4390 DATA "WALL AFTER WALL OF EVIL BOOKS"
 4400 DATA "A NUMBER OF SOFTER ROOTS"
 4410 DATA "FIERCE LIVING STORM THAT FOLLOWS YOU"
 4420 DATA "MALEVOLENT WRAITHS WHO PUSH YOU TOWARD THE WELL"
 4430 DATA "HIS DREADED CLOAK OF ENTROPY"
 4440 DATA "OMEGAN THE EVIL ONE"
 4450 DATA "AN IMMENSE SNAKE WOUND AROUND THE HUT"
 4460 DATA "A GROUP OF AGGRESSIVE LOGMEN"
 4470 DATA "THE ANCIENT SCAVENGER","MEDIAN"
 4480 GOSUB 3150
 4490 DATA "BY","FACING","AT","IN","OUTSIDE","BENEATH","ON"
 4500 FOR I = 1 TO 43 : READ TP$ : NEXT I
 4510 FOR I = 1 TO 7 : READ I$(I) : NEXT I
 4520 R=23 : B=8 : L=1000 : E=0
 4530 C1=16 : C2=21 : C3=24 : F=2 : G=2
 4540 F=2 : G= 2
 4550 Y=100 : X= 35
 4560 H$= "MNgIL5;/U^kZpcL%LJ\5LJm-ALZ/SkIngRm73**MJFF          "
 4570 Q$= "90101191001109109000901000111000000100000010000000000"
 4580 G$= "----------------------------------------"
 4590 F$= "LET YOUR QUEST BEGIN"
 4600 K$= "REFLECTIONS STIR WITHIN"
 4610 L$= "REMEMBER ALADDIN IT WORKED FOR HIM"
 4620 M$= "DECIDE TO HAVE A LITTLE FUN AND "
 4630 N$= "THE TORCH BRIGHTENS"
 4640 U$= "YOU ANGER THE BIRD"
 4650 W$= "YOU CAN'T "
 4660 P$= "REMEMBER OLD TIMES"
 4670 R$= " WHICH FLIES YOU TO A REMOTE PLACE"
 4680 S$= "FALLING UNDER THE SPELL OF THE BOATMAN "
 4690 T$= "AND ARE TAKEN TO THE ISLAND OF SECRETS"
 4700 J$= "THE STONES ARE FIXED"
 4710 O$= "WHICH TAKES OMEGAN IN ITS CLAWS AND FLIES AWAY"
 4720 V$= "N??S??E??W??GO?GETTAKGIVDROLEAEATDRIRIDOPEPICCHOCHITAPBREFIGSTRATT"
 4730 V$=V$ + "HITKILSWISHEHELSCRCATRUBPOLREAEXAFILSAYWAIRESWAVINFXLOXSAQUI"
 4740 Z$= "APPEGGFLOJUGRAGPARTORPEBAXEROPSTACHICOAFLIHAMCANLOAMELBISMUS"
 4750 Z$=Z$ + "BOTWINSAPWATBOACHECOLSTOTRAVILLIQSWASAGBOOROOASAWRACLOOMESNA"
 4760 Z$=Z$ + "LOGSCAMEDNORSOUEASWESUP?DOWIN?OUT???"
 4770 FOR I= 1 TO W +1
 4780   L(I)=ASC(MID$(H$,I,1))-32 : F(I)=ASC(MID$(Q$,I,1))-48
 4790 NEXT I
 4800 H$= "STONY WORDS"
 4810 DEF FNR(Z)=INT(RND(0)*Z+1)
 4820 DEF FNP(Z)=POS(0)
 4830 DEF FNS(Z)=Y-(E/C4+.1)
 4840 REM RETURN
 4850 ENDPROC : REM end initialise
 4860 :
 4870 REM *FX138,0,128
 4880 :
 4890 DEF PROC_load_chars
 4900 REM ASSIGN O to CHARS.
 4910 VDU 23,229,0,2,6,15,30,60,56,120
 4920 VDU 23,230,0,96,96,120,124,28,30,14
 4930 VDU 23,231,120,120,120,120,56,60,30,14
 4940 VDU 23,232,14,14,14,30,30,60,120,112
 4950 REM ASSIGN S to CHARS.
 4960 VDU 23,233,0,7,15,31,63,62,60,31
 4970 VDU 23,234,0,16,188,28,30,12,0,128
 4980 VDU 23,235,15,3,0,0,56,56,60,30
 4990 VDU 23,236,240,248,124,60,60,120,240,224
 5000 REM HUD CHARS.
 5010 REM ASSIGN I to CHARS.
 5020 VDU 23,237,0,24,57,113,3,3,3,3
 5030 VDU 23,238,0,244,248,252,192,192,192,192
 5040 VDU 23,239,3,3,3,3,3,95,63,95
 5050 VDU 23,240,192,192,192,192,192,140,156,56
 5060 REM FULL BLOCK
 5070 VDU 23,241,0,0,0,0,28,28,28,28
 5080 ENDPROC : REM end load chars
 5090 :
 5100 DEF PROC_drawIOS(T2X,T2Y,ColTop%,ColBot%)  : REM DRAW IOS.
 5110 REM DISPLAY I (2x2) CHARS.
 5120 :
 5130 REM DISPLAY I (2x2) CHARS.
 5140 COLOUR ColTop% : PRINT TAB(T2X+0,T2Y);CHR$(237);CHR$(238)
 5150 COLOUR ColBot% : PRINT TAB(T2X+0,T2Y+1);CHR$(239);CHR$(240)
 5160 COLOUR ColBot% : PRINT TAB(T2X+2,T2Y+1);CHR$(241) : REM solid block
 5170 REM DISPLAY O (2x2) CHARS.
 5180 COLOUR ColTop% : PRINT TAB(T2X+3,T2Y);CHR$(229);CHR$(230)
 5190 COLOUR ColBot% : PRINT TAB(T2X+3,T2Y+1);CHR$(231);CHR$(232)
 5200 COLOUR ColBot% : PRINT TAB(T2X+5,T2Y+1);CHR$(241) : REM solid block
 5210 REM DISPLAY S (2x2) CHARS.
 5220 COLOUR ColTop% : PRINT TAB(T2X+6,T2Y);CHR$(233);CHR$(234)
 5230 COLOUR ColBot% : PRINT TAB(T2X+6,T2Y+1);CHR$(235);CHR$(236)
 5240 COLOUR White%
 5250 ENDPROC : REM end draw ios.
 5260 :
 5270 DEF PROC_load_vars : REM VARS *********************************************************
 5280 MODE 8
 5290 REM colours.
 5300 Black%=0
 5310 Red%=1
 5320 Green%=2
 5330 Yellow%=3
 5340 Blue%=4
 5350 Magenta%=5
 5360 Cyan%=6
 5370 LtGray%=7
 5380 DkGray%=8
 5390 IntRed%=9
 5400 IntGreen%=10
 5410 IntYellow%=11
 5420 IntBlue%=12
 5430 IntMagenta%=13
 5440 IntCyan%=14
 5450 White%=15
 5460 MB%=&40000 : REM Memory Bank &40000.
 5470 SW%=1279 : SH%=1023 : STW%=40 : STH%=30 : T1X=0 : T1Y=0 : T2X=0 : T2Y=0 : HSPLITY%=SH%-140 : REM screen width & height in graphical units.
 5480 COLOUR 128+Black%
 5490 COLOUR White%
 5500 HudCol% = IntGreen%
 5510 PrimCol% = IntGreen%
 5520 SecCol% = IntYellow%
 5530 Text1Col% = IntGreen%
 5540 Text2Col% = IntYellow%
 5550 LT% = 1 : RT% = STW%-2
 5560 ENDPROC : REM end vars
 5570 :
 5580 DEF PROC_messageViewPort
 5590 VDU 28,LT%,STH%-2,RT%,5 : REM SET text VP.L,B,R,T
 5600 REM COLOUR 128+DkGray%
 5610 CLS
 5620 ENDPROC
 5630 :
 5640 DEF PROC_locationViewPort
 5650 VDU 28,LT%,10,RT%,6 : REM SET text VP.
 5660 ENDPROC
 5670 :
 5680 DEF PROC_resetViewPorts
 5690 VDU 26
 5700 ENDPROC
 5710 :
 5720 DEF PROC_drawHud(HudCol%) : REM DRAW HUD.
 5730 VDU 18,0,HudCol%
 5740 VDU 26
 5750 MOVE 0,0 : DRAW 0,SH% : MOVE 1,0 : DRAW 1,SH% : MOVE 2,0 : DRAW 2,SH% : REM left border.
 5760 MOVE 0,0 : DRAW SW%,0 : MOVE 0,1 : DRAW SW%,1 : MOVE 0,2 : DRAW SW%,2 : REM bottom border.
 5770 MOVE SW%,0 : DRAW SW%,SH% : MOVE SW%-1,1 : DRAW SW%-1,SH% : MOVE SW%-2,2 : DRAW SW%-2,SH% : REM right border
 5780 MOVE 0,SH% : DRAW SW%,SH% : MOVE 0,SH%-1 : DRAW SW%,SH%-1 : MOVE 0,SH%-2 : DRAW SW%,SH%-2 : REM top border.
 5790 MOVE 0,HSPLITY% : DRAW SW%,HSPLITY% : MOVE 0,HSPLITY%-1 : DRAW SW%,HSPLITY%-1
 5800 MOVE 0,HSPLITY%-2 : DRAW SW%,HSPLITY%-2 : REM horizontal split 1.
 5810 ENDPROC : REM END DRAW HUD.
 5820 :
