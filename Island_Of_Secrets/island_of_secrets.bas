     1 REM ******************************
     2 REM *     ISLAND OF SECRETS      *
     3 REM *  This Version Is For The   *
     4 REM *      **Agon Light2**       *
     5 REM *          MODE: 8           *
     6 REM *     BY STEVE LOVEJOY       *
     7 REM *            2024            *
     8 REM ******************************
     9 :
    10 REM CREDITS:
    11 REM Island Of Secrets was originally
    12 REM written for Usborne Books.
    14 REM It was a type-in listing from
    15 REM back in 1984.
    16 REM I've Ported it to the agon light2
    17 REM
    18 CLEAR
    19 :
    20 PROC_load_vars
    21 PROC_load_chars
    22 PROC_initialise
    24 :
    25 D=R : IF R=20 THEN D=FNR(80)
    26 GOSUB 650
    27 PROC_resetViewPorts
    28 PROC_drawHud(PrimCol%)
    29 PROC_drawIOS(LT%,1,PrimCol%,SecCol%)
    32 :
    44 COLOUR SecCol% : PRINT TAB(11,1) "FOOD:  ";
    45 COLOUR PrimCol% : PRINT F
    46 COLOUR SecCol% : PRINT TAB(11,2) "DRINK: ";
    47 COLOUR PrimCol% : PRINT G
    48 COLOUR SecCol% : PRINT TAB(19,1) "POWER:  ";
    52 COLOUR PrimCol% : PRINT INT(Y)
    54 COLOUR SecCol% : PRINT TAB(19,2) "WISDOM: ";
    56 COLOUR PrimCol% : PRINT X
    58 COLOUR SecCol% : PRINT TAB(30,1) "TIME:";
    59 COLOUR PrimCol% : PRINT L
    60 COLOUR Text1Col%
    61 PROC_messageViewPort
    62 PRINT "YOU ARE "; I$(VAL(LEFT$(A$,1))); " "; : GOSUB 720 : N=0
    70 FOR I = 1 TO C4
    80 C=0 : READ Y$
    90 IF L(I)=R AND F(I)<1 THEN C=1
   100 IF N>0 AND C=1 THEN A$=A$+ ","
   110 IF C=1 THEN A$=A$ + " " + Y$ : N=N+1
   120 NEXT I
   130 IF N>0 THEN A$= "*YOU SEE" + A$ : GOSUB 720
   145 PRINT F$ : REM print message.
   150 PRINT "WHAT WILL YOU DO" : COLOUR Text2Col%
   160 INPUT E$
   170 C$= "" : X$= "" : A=0 : O=52 : LI=LEN(E$)
   180 FOR I = 1 TO LI
   190 IF MID$(E$,I,1)= " " AND C$= "" THEN C$=LEFT$(E$,I-1)
   200 IF MID$(E$,I+1,1)<> " " AND C$> "" THEN X$=RIGHT$(E$,LI-I) : I=LI
   210 NEXT I
   220 IF X$= "" THEN C$=E$
   230 IF LEN(C$)<3 THEN C$=C$+ "???"
   240 FOR I =1 TO V
   250 IF LEFT$(C$,3)=MID$(V$,3*(I-1)+1,3) THEN A=I
   260 NEXT I
   270 GOSUB 760
   280 B$= "" : IF A=0 THEN A=V+1
   290 IF X$= "???" THEN F$= "MOST ACTIONS NEED TWO WORDS"
   300 IF A>V OR O=52 THEN F$=W$+C$+ " " +X$
   310 IF A>V AND O=52 THEN F$= "WHAT!"
   320 L=L-1 : Y=FNS(Z)
   330 B$=STR$(O)+STR$(L(O))+STR$(F(O))+STR$(R)
   340 ON INT(A/10)+1 GOSUB 590,600,610,620,630,630,640
   350 IF R=61 THEN X=X-FNR(2)+1
   360 IF R=14 AND FNR(3)=1 THEN Y=Y-1 : F$= "YOU ARE BITTEN"
   370 IF F(36)<1 AND -R<>F(22) THEN F(36)=F(36)+1 : L(36)=R : Y=Y-1
   380 IF R<>L(16) AND L(16)>0 THEN L(16)=1+FNR(4)
   390 IF R<>L(39) THEN L(39)=10*(FNR(5)-1)+7+FNR(3)
   400 IF R=L(39) AND R<>L(43) AND F(13)>-1 THEN Y=Y-2 : X=X-2
   410 IF R<78 THEN L(32)=76+FNR(2)
   420 IF R=33 OR R=57 OR R=73 AND FNR(2)=1 THEN L(25)=R
   430 IF R=L(32)AND FNR(2)=1 AND F(32)=0 THEN GOSUB 1310
   440 IF R=19 AND Y<70 AND F(43)=0 AND FNR(4)=1 THEN F$= "PUSHED INTO THE PIT" : F(W)=1
   450 IF R<>L(41) THEN L(41)=21+(FNR(3)*10)+FNR(2)
   460 IF R=L(41) THEN F(41)=F(41)-1 : IF F(41)<-4 THEN GOSUB 1230
   470 IF F(43)=0 THEN L(43)=R
   480 IF L(43)<18 AND R<>9 AND R<>10 AND F(W-2)<1 THEN GOSUB 1330
   490 IF R=18 THEN Y=Y-1
   500 IF Y<50 THEN O=FNR(9) : GOSUB 1530 : IF L(O)=R THEN F$= "YOU DROP SOMETHING"
   510 IF L<900 AND R=23 AND F(36)>0 AND FNR(3)=3 THEN GOSUB 1360
   520 IF R=47 AND F(8)>0 THEN F$=F$ + " YOU CAN GO NO FURTHER"
   530 IF F(8)+F(11)+F(13)=-3 THEN F(W)=1 : GOSUB 2800
   540 IF F(W)=0 AND L>0 AND Y>1 AND X>1 THEN GOTO 25
   550 IF L<1 OR Y<1 THEN F$= "YOU HAVE FAILED, THE EVIL ONE SUCCEEDS"
   560 PRINT 'F$ : PRINT "YOUR FINAL SCORE="; INT(X+Y+(ABS(L/7*(L<640))))
   570 PRINT : PRINT : PRINT "GAME OVER"
   580 END : REM end game
   585 :
   590 ON A GOSUB 810,810,810,810,810,1080,1080,1390,1530 : RETURN
   600 ON A-9 GOSUB 1540,1630,1670,1710,1730,1080,1760,1760,1760,1760 : RETURN
   610 ON A-19 GOSUB 1820,1820,1820,1820,1910,2100,2210,2270,2270,1080 : RETURN
   620 ON A-29 GOSUB 2500,2500,2300,2300,2330,2350,2400,2400,2470,2540 : RETURN
   630 ON A-39 GOSUB 2600,2600,2720,640
   640 RETURN
   650 RESTORE
   660 FOR I = 1 TO D : READ A$
   670 NEXT I
   680 D$=RIGHT$(A$,4) : A$=LEFT$(A$,LEN(A$)-4)
   690 IF R=39 THEN D$=MID$( "101110100" ,FNR(5),4)
   700 IF R=20 THEN D$= "1110"
   710 GOSUB 2780 : RETURN
   720 FOR I = 2 TO LEN(A$) : E$=MID$(A$,I,1)
   730 PRINT E$; : IF E$= " " AND FNP(Z)>Z THEN PRINT
   740 NEXT I
   750 PRINT ". " : A$= "" : RETURN
   760 IF LEN(X$)<3 THEN X$=X$+ "???"
   770 FOR I = 1 TO W
   780 IF LEFT$(X$,3)=MID$(Z$,3*(I-1)+1,3) THEN O=I
   790 NEXT I : IF O=0 THEN O=52
   800 RETURN
   810 D=0 : C=0 : IF O=52 THEN D=A
   820 IF O>C4 AND O<W THEN D=O-C4
   830 IF B$= "500012" OR B$= "500053" OR B$= "500045" THEN D=4
   840 IF B$= "500070" OR B$= "500037" OR B$= "510011" OR B$= "510041" THEN D=1
   850 IF B$= "510043" OR B$= "490066" OR B$= "490051" THEN D=1
   860 IF B$= "510060" OR B$= "480056" THEN D=2
   870 IF B$= "510044" OR B$= "510052" THEN D=3
   880 IF B$= "490051" AND F(29)=0 THEN GOSUB 2110 : RETURN
   890 IF R=L(39)AND (X+Y<180 OR R=10)THEN F$=W$ + "LEAVE!" : RETURN
   900 IF R=L(32)AND F(32)<1 AND D=3 THEN F$= "HE WILL NOT LET YOU PAST" : RETURN
   910 IF R=47 AND F(44)=0 THEN F$= "THE ROCKS MOVE TO PREVENT YOU" : RETURN
   920 IF R=28 AND F(7)<>1 THEN F$= "THE ARMS HOLD YOU FAST" : RETURN
   930 IF R=45 AND F(40)=0 AND D=4 THEN F$= "HISSSS!" : RETURN
   940 IF R=25 AND F(16)+L(16)<>-1 AND D=3 THEN F$= "TOO STEEP TO CLIMB" : RETURN
   950 IF R=51 AND D=3 THEN F$= "THE DOOR IS BARRED!" : RETURN
   960 IF D>0 THEN IF MID$(D$,D,1)= "0" THEN R=R+VAL(MID$("-10+10+01-01" ,D*3-2,3)) : C=1
   970 F$= "OK"
   980 IF D<1 OR C=0 THEN F$=W$+ "GO THAT WAY"
   990 IF R=33 AND L(16)=0 THEN L(16)=FNR(4) : F(16)=0 : F$= "THE BEAST RUNS AWAY!"
  1000 IF R<>L(25)OR O<>25 THEN RETURN
  1010 F$= "" : A$= "#YOU BOARD THE CRAFT "
  1020 IF X<60 THEN A$=A$+S$
  1030 A$=A$+T$
  1040 GOSUB 2740 : GOSUB 2760 : GOSUB 2760
  1050 IF X<60 THEN A$= "#TO SERVE OMEGAN FOREVER!" : F(W)=1
  1060 IF X>59 THEN A$= "#THE BOAT SKIMS THE DARK SILENT WATERS" : R=57
  1070 GOSUB 2750 : GOSUB 2760 : GOSUB 2760 : RETURN
  1080 IF((F(O)>0 AND F(O)<9)OR L(O)<>R)AND O<=C3 THEN F$= "WHAT " + X$ + "?" : RETURN
  1090 IF B$= "3450050" THEN Y=Y-8 : X=X-5 : F$= "THEY ARE CURSED" : RETURN
  1100 IF B$= "3810010" THEN GOSUB 1370
  1110 IF(A=15 AND O<>20 AND O<>1) OR (A=29 AND O<>16)OR O>C3 THEN F$=W$ + C$ + " " + X$ : RETURN
  1120 IF L(O)=R AND (F(O)<1 OR F(O)=9) AND O<C3 THEN L(O)=0 : A=-1
  1130 IF O=16 AND L(10)<>0 THEN L(O)=R : F$= "IT ESCAPED" : A=0
  1140 IF O>C1 AND O<C2 THEN F=F+2 : A=-1
  1150 IF O>=C2 AND O<=C3 THEN G=G+2 : A=-1
  1160 IF O>C1 AND O<C3 THEN L(O)=-81
  1170 IF A=-1 THEN F$= "TAKEN" : X=X+4 : E=E+1 : IF F(O)>1 THEN F(O)=0
  1180 IF B$<> "246046" OR L(11)=0 THEN RETURN
  1190 F$=U$ : L(O)=R : IF FNR(3)<3 THEN RETURN
  1200 A$= "#" +U$+R$
  1210 R=63+FNR(6) : L(16)=1 : F$= ""
  1220 GOSUB 2740 : RETURN
  1230 GOSUB 2770 : F$= "" : A$= "#THE LOGMEN " +M$
  1240 F(41)=0 : Y=Y-4 : X=X-4
  1250 IF R<34 THEN A$=A$+ "THROW YOU IN THE WATER" : R=32
  1260 IF R>33 THEN A$=A$+ "TIE YOU UP IN A STOREROOM" : R=51
  1270 GOSUB 2750 : GOSUB 2760
  1280 FOR I =3 TO 4
  1290 IF L(I)=0 THEN L(I)=42
  1300 NEXT I : RETURN
  1310 A$= "*THE SWAMPMAN TELLS HIS TALE"
  1320 GOSUB 2740 : F(32)=-1 : RETURN
  1330 F$= "MEDIAN CAN DISABLE THE EQUIPMENT"
  1340 IF L(8)=0 THEN F$=F$+ " AND ASKS YOU FOR THE PEBBLE YOU CARRY"
  1350 RETURN
  1360 F(36)=-(FNR(4)+6) : F$= "A STORM BREAKS OVERHEAD!" : RETURN
  1370 FOR K=1 TO 30 : GOSUB 2770 : PRINT "////LIGHTNING FLASHES!" : NEXT K
  1380 L(39)=R : Y=Y-8 : X=X-2 : RETURN
  1390 IF (O<>24 AND L(O)>0) OR O=52 THEN F$= "YOU DON'T HAVE THE "+X$ : RETURN
  1400 PRINT "GIVE THE "; X$; " TO WHOM"; : INPUT X$
  1410 Q=O : GOSUB 760 : N=O : O=Q
  1420 IF R<>L(N) THEN F$= "THE " +X$+ " IS NOT HERE" : RETURN
  1430 IF B$= "10045" AND N=40 THEN LET L(O)=81 : F(40)=1 : F$= "THE SNAKE UNCURLS"
  1440 IF B$= "2413075" AND N=30 AND G>1 THEN F(11)=0 : F$= "HE OFFERS HIS STAFF" : G=G-1
  1450 B$=LEFT$(B$,3):F$= "IT IS REFUSED"
  1460 IF B$= "300" AND N=42 THEN X=X+10 : L(O)=81
  1470 IF B$= "120" AND N=42 THEN X=X+10 : L(O)=81
  1480 IF B$= "40-" AND N=32 THEN F(N)=1 : L(O)=81
  1490 IF LEFT$(B$,2)= "80" AND N=43 THEN L(O)=81 : GOSUB 1560
  1500 IF L(O)=81 OR (O=24 AND L(11)>0 AND G>0) THEN F$= "IT IS ACCEPTED"
  1510 IF N=41 THEN L(O)=51 : F$= "IT IS TAKEN"
  1520 RETURN
  1530 IF O=4 AND L(O)= 0 THEN L(O)=81 : X=X-1 : F$= "IT BREAKS!" : RETURN
  1540 IF L(O)=0 AND O<=C1 THEN L(O)=R : F$= "DONE" : E=E-1
  1550 RETURN
  1560 A$= "*HE TAKES IT " : IF R<>8 THEN A$=A$+ "RUNS DOWN THE CORRIDOR,"
  1570 GOSUB 2740 : A$= "*AND CASTS IT INTO THE CHEMICAL VATS, PURIFYING THEM WITH"
  1580 A$=A$+ " A CLEAR BLUE LIGHT REACHING FAR INTO THE LAKES AND RIVERS BEYOND"
  1590 F(8)=-1 : GOSUB 2750 : GOSUB 2760 : GOSUB 2760 : RETURN
  1600 IF L(I)<>0 AND I<C1 THEN I=I+1 : GOTO 1600
  1610 IF L(I)=0 THEN L(I)=R : F(I)=0 : GOSUB 1540 : F$= "YOU DROP SOMETHING"
  1620 RETURN
  1630 IF(O<C1 OR O>C3) AND X$<> "???" THEN F$=W$+C$+ " " +X$:X=X-1 : RETURN
  1640 F$= "YOU HAVE NO FOOD" : IF F>0 THEN F=F-1 : Y=Y+10 : F$= "OK"
  1650 IF O=3 THEN X=X-5:Y=Y-2 : F$= "THEY MAKE YOU VERY ILL!"
  1660 RETURN
  1670 IF O=31 THEN GOSUB 2380 : RETURN
  1680 IF X$<> "???" AND (O<21 OR O>C3) THEN F$=W$+C$+ " " +X$ : X=X-1 : RETURN
  1690 F$= "YOU HAVE NO DRINK" : IF G>0 THEN G=G-1 : Y=Y+7 : F$= "OK"
  1700 RETURN
  1710 IF LEFT$(B$,4)= "1600" THEN F(O)=-1 : F$= "IT ALLOWS YOU TO RIDE"
  1720 RETURN
  1730 IF B$= "2644044" THEN F$= "CHEST OPEN" : F(6)=9 : F(5)=9 : F(15)=9
  1740 IF B$= "2951151" THEN F$= "THE TRAPDOOR CREAKS" : F(29)=0 : X=X+3
  1750 RETURN
  1760 Y=Y-2 : IF B$= "3577077" AND L(9)=0 THEN F(23)=0 : L(23)=R
  1770 IF V>15 AND V<19 AND (L(9)=0 OR L(15)=0) THEN F$= "OK"
  1780 IF B$= "1258158" OR B$= "2758158" AND L(15)=0 THEN F(12)=0 : F(27)=0 : F$= "CRACK!"
  1790 IF LEFT$(B$,4)= "1100" AND R=10 THEN GOSUB 1980
  1800 IF A=18 AND (O>29 AND O<34) OR (O>38 AND O<44) OR O=16 THEN GOSUB 1900
  1810 RETURN
  1820 Y=Y-2 : X=X-2 : IF R<>L(O) AND L(O)<>0 THEN RETURN
  1830 IF O=39 THEN F$= "HE LAUGHS DANGEROUSLY"
  1840 IF O=32 THEN F$= "THE SWAMPMAN IS UNMOVED"
  1850 IF O=33 THEN F$=W$+ "TOUCH HER!" : L(3)=81
  1860 IF O=41 THEN F$= "THEY THINK THAT'S FUNNY! "
  1870 IF R=46 GOSUB 1200
  1880 IF LEFT$(B$,4)= "1400" AND R=L(39) THEN GOSUB 1980
  1890 Y=Y-8 : X=X-5 : RETURN
  1900 IF L(9)>0 THEN RETURN
  1910 Y=Y-12 : X=X-10 : F$= "THAT WOULD BE UNWISE!"
  1920 IF R<>L(O) THEN RETURN
  1930 F(W)=1 : A$= "#THUNDER SPLITS THE SKY! " : F$=""
  1940 A$=A$+ "IT IS THE TRIUMPHANT VOICE OF OMEGAN." : GOSUB 2740
  1950 A$= "#WELL DONE ALPHAN! THE MEANS BECOME THE END.. "
  1960 A$=A$+ "I CLAIM YOU AS MY OWN! HA HA HAH!" : GOSUB 2750
  1970 GOSUB 2760 : X=0 : L=0 : Y=0 : RETURN
  1980 GOSUB 2770 : ON O-10 GOSUB 2010,2060,2060,2060
  1990 X=X+10:L(O)=81 : F(O)=-1 : GOSUB 720 : GOSUB 2760 : GOSUB 2760
  2000 RETURN
  2010 A$= "#IT SHATTERS RELEASING A DAZZLING RAINBOW OF COLOURS!"
  2020 IF L(2)<>R THEN RETURN
  2030 A$=A$+ "THE EGG HATCHES INTO A BABY DACTYL " +O$
  2040 L(39)=81 : L(2)=81 : F(2)=-1 : Y=Y+40
  2050 RETURN
  2060 IF L(13)<>R THEN RETURN
  2070 A$= "*THE COAL BURNS WITH A WARM RED FLAME" : F(13)=-1
  2080 IF R=10 AND R=L(39) THEN A$=A$+ " WHICH DISSOLVES OMEGAN'S CLOAK" : Y=Y+20
  2090 RETURN
  2100 IF R<>51 OR F(29)>0 THEN F$=W$+C$+ " HERE" : X=X+1
  2110 X=X-1 : R=FNR(5) : GOSUB 2770 : PRINT "SWIMMING IN THE POISONOUS WATERS"
  2120 J=0 : B$="" : F$= "YOU SURFACE" : PRINT "YOUR STRENGTH = "; INT(Y)
  2130 FOR I=1 TO R
  2140 IF Y<15 THEN PRINT "YOU ARE VERY WEAK"
  2150 PRINT "WHICH WAY"; : INPUT X$ : X$=LEFT$(X$,1) : B$=B$+X$ : NEXT I
  2160 FOR I=1 TO R
  2170 Y=FNS(Z)-3 : IF MID$(B$,I,1)="N" THEN J=J+1
  2180 NEXT I  : IF R/2>J AND Y>1 THEN 2110
  2190 IF Y<2 THEN F$= "YOU GOT LOST AND DROWNED"
  2200 R=30+FNR(3) : RETURN
  2210 IF F(36)>-1 THEN RETURN
  2220 GOSUB 2770 : PRINT "YOU CAN RUN TO SHELTER IN:" : PRINT "1) GRANDPA'S SHACK"
  2230 PRINT "2) CAVE OF SNELM"'"3) LOG CABIN" : PRINT "CHOOSE FROM 1-3" : INPUT A$
  2240 IF A$>"0" AND A$<"4" THEN R=ASC(MID$("A >",VAL(A$),1))-21 : F(22)=-R
  2250 PRINT "YOU RUN BLINDLY THROUGH THE STORM" : F$= "YOU REACH SHELTER"
  2260 GOSUB 2760 : RETURN
  2270 IF B$= "3075075" OR B$= "3371071" THEN F$= "HOW WILL YOU DO THAT"
  2280 IF B$= "3371071" AND A=28 THEN F(3)=0 : F$= "SHE NODS SLOWLY" : X=X+5
  2290 RETURN
  2300 F$= "EXAMINE THE BOOK FOR CLUES"
  2310 IF LEFT$(B$,3)= "600" THEN F$=L$
  2320 RETURN
  2330 IF B$= "40041" THEN F(4)=-1 : F$= "FILLED"
  2340 RETURN
  2350 F$=X$ : IF X$=H$ AND R=47 AND F(8)=0THEN F(44)=1 : F$=J$
  2360 IF X$<>P$ OR R<>L(42) OR L(3)<81 OR L(12)<81 THEN RETURN
  2370 F$= "HE EATS THE FLOWERS - AND CHANGES" : F(42)=1 : F(43)=0 : RETURN
  2380 IF F(4)+L(4)<>-1 THEN F$= "YOU DON'T HAVE " +X$ : RETURN
  2390 GOSUB 2770 : PRINT "YOU TASTE A DROP AND..." : GOSUB 2760 : F$= "*OUCH!" : Y=Y-4 : X=X-7
  2400 GOSUB 2270 : FOR I=1 TO ABS(F(36))+3
  2410 L=L-1 : IF Y<100 OR -R=F(22) THEN Y=Y+1
  2420 PRINT "TIME PASSES..." : GOSUB 2760
  2430 NEXT I
  2440 IF L>100 OR F(36)<1 THEN X=X+2 : F(36)=1
  2450 IF A=37 OR A=36 THEN F$="OK"
  2460 RETURN
  2470 IF R=L(25)THEN F$= "THE BOATMAN WAVES BACK"
  2480 IF LEFT$(B$,3)= "700" THEN F(7)=1 : F$=N$ : X=X+8
  2490 RETURN
  2500 F$= "A-DUB-DUB" : IF LEFT$(B$,4)<>"2815" THEN RETURN
  2510 IF F(O)=1 THEN F(O)=0 : F$=K$ : RETURN
  2520 IF L(5)=0 THEN F(8)=0 : GOSUB 1080 : F$= "THE STONE UTTERS " +H$
  2530 RETURN
  2540 GOSUB 2770 : PRINT " INFO - ITEMS CARRIED" : GOSUB 2780
  2550 PRINT G$; TAB(0); " FOOD="; F; TAB(23); "DRINK="; G : PRINT G$; : F$= "OK"
  2560 FORI=1 TO C4
  2570 READ Y$ : IF L(I)=0 THEN PRINT Y$
  2580 NEXT I
  2590 PRINT G$; : GOSUB 2730 : RETURN
  2600 C$="LOAD" : IF A=41 THEN C$= "SAVE"
  2610 PRINT "PREPARE TO "; C$ : GOSUB 2730
  2620 IF A=40 THEN FL=OPENIN "ISDATA"
  2630 IF A=41 THEN FL=OPENOUT "ISDATA"
  2640 IF A=41 THEN F(50)=R:F(49)=Y:F(48)=X:F(47)=F:F(46)=G:F(45)=L
  2650 FOR I=1 TO W
  2660 IF A=40 THEN INPUT #FL,L(I),F(I)
  2670 IF A=41 THEN PRINT #FL,L(I),F(I)
  2680 NEXT I
  2690 CLOSE#FL
  2700 IF A=40 THEN R=F(50):Y=F(49):X=F(48):F=F(47):G=F(46):L=F(45)
  2710 F$="OK" : RETURN
  2720 F(W)=-1 : F$= "YOU RELINQUISH YOUR QUEST." : L=1 : RETURN
  2730 INPUT"PRESS ENTER";A$ : RETURN
  2740 GOSUB 2770
  2750 GOSUB 720 : GOSUB 2760 : RETURN
  2760 FOR D= 1 TO 2000 : NEXT D : RETURN
  2770 CLS : RETURN
  2780 RESTORE : FOR I = 1 TO 80 : READ TP$ : NEXT I
  2790 RETURN
  2800 A$= "*THE WORLD LIVES WITH NEW HOPE!" : GOSUB 2750
  2810 F$= "YOUR QUEST IS OVER" : REM  RETURN
  2815 :
  2817 DEF PROC_initialise
  2820 :
  2830 LR=2860 : Z=60 : Z=INT(Z*.8)
  2840 V=42 : W=51 : C4=43
  2850 DIM I$(7)
  2860 DIM L(52),F(52)
  2870 DATA "4THE FURTHEST DEPTHS OF THE FOREST1001"
  2880 DATA "4THE DEPTHS OF THE MUTANT FOREST1000"
  2890 DATA "7A PATH OUT OF THE OVERGROWN DEPTHS1000"
  2900 DATA "6A CARNIVOROUS TREE1000"
  2910 DATA "4A CORRAL BENEATH THE CRIMSON CANYON1110"
  2920 DATA "7THE TOP OF A STEEP CLIFF1011"
  2930 DATA "4THE MARSH FACTORY1001"
  2940 DATA "4THESLUDGE FERMENTATION VATS1110"
  2950 DATA "7THE UPPERMOST BATTLEMENTS1001"
  2960 DATA "4OMEGAN'S SANCTUM1110"
  2970 DATA "4SNELM'S LAIR0001"
  2980 DATA "2A DARK CAVE0000"
  2990 DATA "1BROKEN BRANCHES0100"
  3000 DATA "1A THICKET OF BITING BUSHES0000"
  3010 DATA "1A HUGE GLASSY STONE1110"
  3020 DATA "7THE EDGE OF CRIMSON CANYON0011"
  3030 DATA "4THE CLONE FACTORY0101"
  3040 DATA "4A CORRIDOR OF CLONE STORAGE CASKS1100"
  3050 DATA "7EDGE OF THE WELL0000"
  3060 DATA "4THE ROOM OF SECRET VISIONS1110"
  3070 DATA "4SNELM'S INNER CHAMBER0111"
  3080 DATA "3THE SOUTHERN EDGE OF THE FOREST0101"
  3090 DATA "7A LEAFY PATH1000"
  3100 DATA "3A FORK IN THE PATH0100"
  3110 DATA "7AN APPARENTLY UNCLIMBABLE ROCKY PATH1100"
  3120 DATA "7A LEDGE ATOP THE CRIMSON CANYON0010"
  3130 DATA "4A TALL ENTRANCE CHAMBER1101"
  3140 DATA "4A LOW PASSAGE WITH ARMS REACHING FROM THE WALLS1010"
  3150 DATA "7THE APPROACH TO THE WELL OF DESPAIR0001"
  3160 DATA "4A DIM CORRIDOR DEEP IN THE CASTLE1010"
  3170 DATA "4THE STAGNANT WATERS OF THE CRAWLING CREEK1001"
  3180 DATA "4A SHALLOW POOL OFF THE CREEK1100"
  3190 DATA "7A LOG PIER, JUTTING OUT OVER THE CREEK0000"
  3200 DATA "4A STRETCH OF FEATURELESS DUNES1100"
  3210 DATA "1A GROUP OF TALL TREES1010"
  3220 DATA "7A NARROW LEDGE AT THE SUMMIT OF THE CANYON0011"
  3230 DATA "2A MONSTROUS PORTAL IN THE CASTLE WALL0011"
  3240 DATA "4A CHAMBER INCHES DEEP WITH DUST0001"
  3250 DATA "4HERE1111"
  3260 DATA "2A CARVED ARCHWAY0010"
  3270 DATA "4A SMALL HUT IN THE LOG SETTLEMENT0111"
  3280 DATA "1A HUGE SPLIT-LOG TABLE1001"
  3290 DATA "4THE PORCH OF THE LOGMEN'S CABIN0110"
  3300 DATA "4GRANDPA'S SHACK1101"
  3310 DATA "3A CLEARING IN THE TREES BY A RICKETY SHACK0010"
  3320 DATA "4THE NEST OF A HUGE DACTYL0111"
  3330 DATA "6THE CASTLE OF DARK SECRETS BY TWO HUGE STONES0011"
  3340 DATA "4A ROOM LITTERED WITH BONES0111"
  3350 DATA "4THE CELL OF WHISPERED SECRETS0111"
  3360 DATA "4THE LIBRARY OF WRITTEN SECRETS0111"
  3370 DATA "4A REFUSE STREWN STOREROOM 1111"
  3380 DATA "4THE LOGMEN'S HALL0000"
  3390 DATA "5A LOG BUILDING1000"
  3400 DATA "7A RUTTED HILLSIDE1100"
  3410 DATA "7A WINDSWEPT PLAIN AMONGST STONE MEGALITHS0100"
  3420 DATA "7THE STEPS OF AN ANCIENT PYRAMID1010"
  3430 DATA "7THE ISLAND OF SECRETS0111"
  3440 DATA "1A BROKEN MARBLE COLUMN1001"
  3450 DATA "7AN EXPANSE OF CRACKED, BAKED EARTH1100"
  3460 DATA "4A DESERTED ADOBE HUT1010"
  3470 DATA "4A LIVID GROWTH OF MAD ORCHIDS1011"
  3480 DATA "4A CORNER STREWN WITH BROKEN CHAIRS0111"
  3490 DATA "7THE BRIDGE NEAR TO A LOG SETTLEMENT0011"
  3500 DATA "1A CRUMBLING MASS OF PETRIFIED TREES1011"
  3510 DATA "3THE EDGE OF THE PYRAMID1101"
  3520 DATA "7THE ROOF OF THE ANCIENT PYRAMID0100"
  3530 DATA "3AN IMPASSABLE SPLIT IN THE PYRAMID1110"
  3540 DATA "7A BARREN BLASTED WASTELAND0001"
  3550 DATA "4AN EXPANSE OF BLEAK, BURNT LAND1100"
  3560 DATA "5A DILAPIDATED ADOBE HUT0110"
  3570 DATA "4THE HEART OF THE LILIES0101"
  3580 DATA "4THE MIDST OF THE LILIES1100"
  3590 DATA "3A RIVER'S EDGE BY A LOG BRIDGE0100"
  3600 DATA "3A PETRIFIED VILLAGE BY A RIVER CROWDED WITH LILIES0100"
  3610 DATA "4THE REMAINS OF A VILLAGE1100"
  3620 DATA "3THE ENTRANCE TO A PETRIFIED VILLAGE1100"
  3630 DATA "4A SWAMP MATTED WITH FIBROUS ROOTS1100"
  3640 DATA "2A VILLAGE OF HOLLOW STUMPS DEFYING THE SWAMP0100"
  3650 DATA "4A TUNNEL INTO ONE OF THE TREE STUMPS1100"
  3660 DATA "4A HOLLOW CHAMBER MANY METRES IN DIAMETER1110"
  3670 DATA "A SHINY APPLE"
  3680 DATA "A FOSSILISED EGG"
  3690 DATA "A LILY FLOWER"
  3700 DATA "AN EARTHENWARE JUG"
  3710 DATA "A DIRTY OLD RAG"
  3720 DATA "A RAGGED PARCHMENT"
  3730 DATA "A FLICKERING TORCH"
  3740 DATA "A GLISTENING APPLE"
  3750 DATA "A WOODMAN'S AXE"
  3760 DATA "A COIL OF ROPE"
  3770 DATA "A RUGGED STAFF"
  3780 DATA "A CHIP OF MARBLE"
  3790 DATA "A POLISHED COAL"
  3800 DATA "A PIECE OF FLINT"
  3810 DATA "A GEOLOGIST'S HAMMER"
  3820 DATA "A WILD CANYON BEAST"
  3830 DATA "A GRAIN LOAF"
  3840 DATA "A JUICY MELON"
  3850 DATA "SOME BISCUITS"
  3860 DATA "A GROWTH OF MUSHROOMS"
  3870 DATA "A BOTTLE OF WATER"
  3880 DATA "A FLAGON OF WINE"
  3890 DATA "A FLOWING SAP"
  3900 DATA "A SPARKLING FRESHWATER SPRING"
  3910 DATA "THE BOATMAN"
  3920 DATA "A STRAPPED OAK CHEST"
  3930 DATA "A FRACTURE IN THE COLUMN"
  3940 DATA "A MOUTH-LIKE OPENING"
  3950 DATA "AN OPEN TRAPDOOR"
  3960 DATA "A PARCHED, DESSICATED VILLAGER"
  3970 DATA "A STILL OF BUBBLING GREEN LIQUOR"
  3980 DATA "A TOUGH SKINNED SWAMPMAN"
  3990 DATA "THE SAGE OF THE LILIES"
  4000 DATA "WALL AFTER WALL OF EVIL BOOKS"
  4010 DATA "A NUMBER OF SOFTER ROOTS"
  4020 DATA "FIERCE LIVING STORM THAT FOLLOWS YOU"
  4030 DATA "MALEVOLENT WRAITHS WHO PUSH YOU TOWARD THE WELL"
  4040 DATA "HIS DREADED CLOAK OF ENTROPY"
  4050 DATA "OMEGAN THE EVIL ONE"
  4060 DATA "AN IMMENSE SNAKE WOUND AROUND THE HUT"
  4070 DATA "A GROUP OF AGGRESSIVE LOGMEN"
  4080 DATA "THE ANCIENT SCAVENGER","MEDIAN"
  4090 GOSUB 2780
  4100 DATA "BY","FACING","AT","IN","OUTSIDE","BENEATH","ON"
  4110 FOR I = 1 TO 43 : READ TP$ : NEXT I
  4120 FOR I = 1 TO 7 : READ I$(I) : NEXT I
  4130 R=23 : B=8 : L=1000 : E=0
  4140 C1=16 : C2=21 : C3=24 : F=2 : G=2
  4150 F=2 : G= 2
  4160 Y=100 : X= 35
  4170 H$= "MNgIL5;/U^kZpcL%LJ\5LJm-ALZ/SkIngRm73**MJFF          "
  4180 Q$= "90101191001109109000901000111000000100000010000000000"
  4190 G$= "----------------------------------------"
  4200 F$= "LET YOUR QUEST BEGIN"
  4210 K$= "REFLECTIONS STIR WITHIN"
  4220 L$= "REMEMBER ALADDIN IT WORKED FOR HIM"
  4230 M$= "DECIDE TO HAVE A LITTLE FUN AND "
  4240 N$= "THE TORCH BRIGHTENS"
  4250 U$= "YOU ANGER THE BIRD"
  4260 W$= "YOU CAN'T "
  4270 P$= "REMEMBER OLD TIMES"
  4280 R$= " WHICH FLIES YOU TO A REMOTE PLACE"
  4290 S$= "FALLING UNDER THE SPELL OF THE BOATMAN "
  4310 T$= "AND ARE TAKEN TO THE ISLAND OF SECRETS"
  4320 J$= "THE STONES ARE FIXED"
  4330 O$= "WHICH TAKES OMEGAN IN ITS CLAWS AND FLIES AWAY"
  4340 V$= "N??S??E??W??GO?GETTAKGIVDROLEAEATDRIRIDOPEPICCHOCHITAPBREFIGSTRATT"
  4350 V$=V$ + "HITKILSWISHEHELSCRCATRUBPOLREAEXAFILSAYWAIRESWAVINFXLOXSAQUI"
  4360 Z$= "APPEGGFLOJUGRAGPARTORPEBAXEROPSTACHICOAFLIHAMCANLOAMELBISMUS"
  4370 Z$=Z$ + "BOTWINSAPWATBOACHECOLSTOTRAVILLIQSWASAGBOOROOASAWRACLOOMESNA"
  4380 Z$=Z$ + "LOGSCAMEDNORSOUEASWESUP?DOWIN?OUT???"
  4390 FOR I= 1 TO W +1
  4400 L(I)=ASC(MID$(H$,I,1))-32 : F(I)=ASC(MID$(Q$,I,1))-48
  4410 NEXT I
  4420 H$= "STONY WORDS"
  4430 DEF FNR(Z)=INT(RND(0)*Z+1)
  4440 DEF FNP(Z)=POS(0)
  4450 DEF FNS(Z)=Y-(E/C4+.1)
  4460 REM RETURN
  4465 ENDPROC : REM end initialise
  4467 :
  4480 REM *FX138,0,128
  5000 :
  5010 DEF PROC_load_chars
  6590 REM ASSIGN O to CHARS.
  6600 VDU 23,229,0,2,6,15,30,60,56,120
  6610 VDU 23,230,0,96,96,120,124,28,30,14
  6620 VDU 23,231,120,120,120,120,56,60,30,14
  6630 VDU 23,232,14,14,14,30,30,60,120,112
  6640 REM ASSIGN S to CHARS.
  6650 VDU 23,233,0,7,15,31,63,62,60,31
  6660 VDU 23,234,0,16,188,28,30,12,0,128
  6670 VDU 23,235,15,3,0,0,56,56,60,30
  6680 VDU 23,236,240,248,124,60,60,120,240,224
  6690 REM HUD CHARS.
  6700 REM ASSIGN I to CHARS.
  6710 VDU 23,237,0,24,57,113,3,3,3,3
  6720 VDU 23,238,0,244,248,252,192,192,192,192
  6730 VDU 23,239,3,3,3,3,3,95,63,95
  6740 VDU 23,240,192,192,192,192,192,140,156,56
  6742 REM FULL BLOCK
  6744 VDU 23,241,0,0,0,0,28,28,28,28
  6750 ENDPROC : REM end load chars
  6760 :
  6800 DEF PROC_drawIOS(T2X,T2Y,ColTop%,ColBot%)  : REM DRAW IOS.
  6830 REM DISPLAY I (2x2) CHARS.
  6840 :
  6850 REM DISPLAY I (2x2) CHARS.
  7450 COLOUR ColTop% : PRINT TAB(T2X+0,T2Y);CHR$(237);CHR$(238)
  7460 COLOUR ColBot% : PRINT TAB(T2X+0,T2Y+1);CHR$(239);CHR$(240)
  7465 COLOUR ColBot% : PRINT TAB(T2X+2,T2Y+1);CHR$(241) : REM solid block
  7470 REM DISPLAY O (2x2) CHARS.
  7480 COLOUR ColTop% : PRINT TAB(T2X+3,T2Y);CHR$(229);CHR$(230)
  7490 COLOUR ColBot% : PRINT TAB(T2X+3,T2Y+1);CHR$(231);CHR$(232)
  7495 COLOUR ColBot% : PRINT TAB(T2X+5,T2Y+1);CHR$(241) : REM solid block
  7500 REM DISPLAY S (2x2) CHARS.
  7510 COLOUR ColTop% : PRINT TAB(T2X+6,T2Y);CHR$(233);CHR$(234)
  7520 COLOUR ColBot% : PRINT TAB(T2X+6,T2Y+1);CHR$(235);CHR$(236)
  7525 COLOUR White%
  7530 ENDPROC : REM end draw ios.
  7540 :
  7550 DEF PROC_load_vars : REM VARS *********************************************************
  7560 MODE 8
  7570 REM colours.
  7580 Black%=0
  7590 Red%=1
  7595 Green%=2
  7597 Yellow%=3
  7600 Blue%=4
  7602 Magenta%=5
  7608 Cyan%=6
  7610 LtGray%=7
  7612 DkGray%=8
  7614 IntRed%=9
  7616 IntGreen%=10
  7620 IntYellow%=11
  7630 IntBlue%=12
  7635 IntMagenta%=13
  7640 IntCyan%=14
  7650 White%=15
  7660 MB%=&40000 : REM Memory Bank &40000.
  7665 SW%=1279 : SH%=1023 : STW%=40 : STH%=30 : T1X=0 : T1Y=0 : T2X=0 : T2Y=0 : VSPLITX%=(SW%/4)*3 : HSPLITY%=SH%/4 : HSPLITY2%=SH%-140 : REM screen width & height in graphical units.
  7710 COLOUR 128+Black%
  7720 COLOUR White%
  7725 HudCol% = IntGreen%
  7726 PrimCol% = IntGreen%
  7727 SecCol% = IntYellow%
  7728 Text1Col% = IntGreen%
  7729 Text2Col% = IntYellow%
  7730 LT% = 1 : RT% = ((STW%/4)*3)
  8000 ENDPROC : REM end vars
  8010 :
  8020 DEF PROC_messageViewPort
  8030 VDU 28,LT%,((STH%/4)*3)-1,RT%-2,5 : REM SET text VP.L,B,R,T
  8032 REM COLOUR 128+DkGray%
  8035 CLS
  8040 ENDPROC
  8050 :
  8060 DEF PROC_locationViewPort
  8070 VDU 28,LT%,10,RT%-2,6 : REM SET text VP.
  8080 ENDPROC
  8090 :
  8100 DEF PROC_resetViewPorts
  8110 VDU 26
  8120 ENDPROC
  8125 :
  8130 DEF PROC_drawHud(HudCol%) : REM DRAW HUD.
  8140 VDU 18,0,HudCol%
  8150 VDU 26
  8160 MOVE 0,0 : DRAW 0,SH% : MOVE 1,0 : DRAW 1,SH% : MOVE 2,0 : DRAW 2,SH% : REM left border.
  8170 MOVE 0,0 : DRAW SW%,0 : MOVE 0,1 : DRAW SW%,1 : MOVE 0,2 : DRAW SW%,2 : REM bottom border.
  8180 MOVE SW%,0 : DRAW SW%,SH% : MOVE SW%-1,1 : DRAW SW%-1,SH% : MOVE SW%-2,2 : DRAW SW%-2,SH% : REM right border
  8190 MOVE 0,SH% : DRAW SW%,SH% : MOVE 0,SH%-1 : DRAW SW%,SH%-1 : MOVE 0,SH%-2 : DRAW SW%,SH%-2 : REM top border.
  8200 MOVE VSPLITX%,0 : DRAW VSPLITX%,SH% : MOVE VSPLITX%-1,0 : DRAW VSPLITX%-1,SH%
  8210 MOVE VSPLITX%-2,0 : DRAW VSPLITX%-2,SH% : REM vertical split.
  8220 MOVE 0,HSPLITY2% : DRAW VSPLITX%,HSPLITY2% : MOVE 0,HSPLITY2%-1 : DRAW VSPLITX%,HSPLITY2%-1
  8230 MOVE 0,HSPLITY2%-2 : DRAW VSPLITX%,HSPLITY2%-2 : REM horizontal split 1.
  8240 MOVE 0,HSPLITY% : DRAW VSPLITX%,HSPLITY% : MOVE 0,HSPLITY%-1 : DRAW VSPLITX%,HSPLITY%-1
  8250 MOVE 0,HSPLITY%-2 : DRAW VSPLITX%,HSPLITY%-2 : REM horizontal split 2.
  8260 ENDPROC : REM END DRAW HUD.
  8270 :
